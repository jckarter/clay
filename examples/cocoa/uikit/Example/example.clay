import cocoa.*;
import cocoa.uikit.*;

overload selector(static #"performMath:") = Void, UISegmentedControl;

record ExampleAppDelegate = newClass(#"ExampleAppDelegate", NSObject,
    InstanceVars(
        [#window, IBOutlet(Retained[UIWindow])],
        [#viewController, IBOutlet(Retained[ExampleViewController])],
    ),
    ClassMethods(),
    InstanceMethods(
        [#"application:didFinishLaunchingWithOptions:", (self, app, options) => {
            self@.window.addSubview(self@.viewController.view());
            self@.window.makeKeyAndVisible();
        }],
        DeallocInstanceMethod(),
    ),
);

record ExampleViewController = newClass(#"ExampleViewController", UIViewController,
    InstanceVars(),
    ClassMethods(),
    InstanceMethods(
        [#"shouldAutorotateToInterfaceOrientation:", (self, orientation) => true],
    ),
);

record ExampleController = newClass(#"ExampleController", NSObject,
    InstanceVars(
        [#leftField,   IBOutlet(UITextField)],
        [#rightField,  IBOutlet(UITextField)],
        [#resultLabel, IBOutlet(UILabel)],
    ),
    ClassMethods(),
    InstanceMethods(
        [#"performMath:", IBAction((self, sender) => {
            switch (Int(sender.selectedSegmentIndex()))
            case (0)
                _performMath(self, add);
            case (1)
                _performMath(self, subtract);
            case (2)
                _performMath(self, multiply);
            case (3)
                _performMath(self, divide);
            else {}
        })],
    ),
);

_performMath(self, fn) {
    var left = self@.leftField.text().intValue();
    var right = self@.rightField.text().intValue();
    self@.resultLabel.setText(NSString.stringWithFormat(NSString(#"%d"), fn(left, right)));
}

main() = uiKitMain(ExampleAppDelegate, ExampleViewController, ExampleController);
