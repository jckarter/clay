
public import __primitives__.(Static);



/// @section  Static? predicate 

define Static?(x) : Bool private overload;
alias override Static?(x) : Bool = false;
[T] alias override Static?(#T) : Bool = true;



/// @section  constructors, moveUnsafe, resetUnsafe, destroy 

[A]
forceinline override Static[A]() --> returned:Static[A] {
}

[A]
forceinline override Static[A](src:Static[A]) --> returned:Static[A] {
}

[A]
forceinline override moveUnsafe(src:Static[A]) --> returned:Static[A] {
}



/// @section  equals?, lesser? 

[A, B]
forceinline override equals?(a:Static[A], b:Static[B]) : Bool = false;

[A]
forceinline override equals?(a:Static[A], b:Static[A]) : Bool = true;

[A]
forceinline override lesser?(a:Static[A], b:Static[A]) : Bool = false;



/// @section  unwrapStatic 

[A]
forceinline unwrapStatic(#A) = A;



/// @section  call 

[F, ..AA when CallDefined?(F, ..AA)]
forceinline override call(#F, forward ..args:AA) =
    forward ..F(..args);



/// @section  fieldRef 

[Module, Ident]
forceinline override fieldRef(#Module, #Ident) =
    forward staticFieldRef(Module, Ident);
