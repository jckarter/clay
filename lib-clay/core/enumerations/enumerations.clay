

/// @section  constructor 

[E when Enum?(E)]
forceinline E() : E = E(0);



/// @section  convert to/from integers 

[E,I when Enum?(E) and Integer?(I)]
forceinline I(a:E) : I = I(enumToInt(a));

[E when Enum?(E)]
forceinline Int(a:E) : Int = enumToInt(a);

[E,I when Enum?(E) and Integer?(I)]
forceinline E(a:I) : E = intToEnum(E, Int(a));

[E when Enum?(E)]
forceinline E(a:Int) : E = intToEnum(E, a);



/// @section  equals?, lesser? 

[E when Enum?(E)]
forceinline equals?(a:E, b:E) : Bool = (enumToInt(a) == enumToInt(b));

[E when Enum?(E)]
forceinline lesser?(a:E, b:E) : Bool = (enumToInt(a) < enumToInt(b));



/// @section  enumName 

[E when Enum?(E)]
enumName(x:E) {
    alias n = EnumMemberCount(E);
    ..for (i in staticIntegers(#n)) {
        if (x == E(unwrapStatic(i)))
            return StringLiteralRef(EnumMemberName(E, i));
    }
    return StringLiteralRef("unknown enum value");
}
