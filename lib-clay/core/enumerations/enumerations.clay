

/// @section  constructor 

[E when Enum?(E)]
inline overload E() = E(0);



/// @section  convert to/from integers 

[E,I when Enum?(E) and Integer?(I)]
inline overload I(a:E) = I(enumToInt(a));

[E when Enum?(E)]
inline overload Int(a:E) = enumToInt(a);

[E,I when Enum?(E) and Integer?(I)]
inline overload E(a:I) = intToEnum(E, Int(a));

[E when Enum?(E)]
inline overload E(a:Int) = intToEnum(E, a);



/// @section  equals?, lesser? 

[E when Enum?(E)]
inline overload equals?(a:E, b:E) = (enumToInt(a) == enumToInt(b));

[E when Enum?(E)]
inline overload lesser?(a:E, b:E) = (enumToInt(a) < enumToInt(b));



/// @section  enumName 

[E when Enum?(E)]
enumName(x:E) {
    alias n = EnumMemberCount(E);
    ..for (i in staticIntegers(#n)) {
        if (x == E(unwrapStatic(i)))
            return StringLiteralRef(EnumMemberName(E, i));
    }
    return StringLiteralRef("unknown enum value");
}
