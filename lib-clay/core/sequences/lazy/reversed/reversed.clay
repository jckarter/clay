
import core.sequences.util.*;


//
// reversed
//

inline reversed(forward seq) = Reversed(wrapSequence(seq));

private record Reversed[A] (
    seq : A
);

[A when LightweightSequence?(A)]
overload LightweightSequence?(#Reversed[A]) = true;

[A when CallDefined?(UnderlyingContainerConstructor, Static[A])]
overload UnderlyingContainerConstructor(#Reversed[A]) =
    UnderlyingContainerConstructor(A);

[A]
inline overload iterator(a:Reversed[A]) = reverseIterator(a.seq);

[A]
inline overload reverseIterator(a:Reversed[A]) = iterator(a.seq);



//
// size, index
//

[A when CallDefined?(size, A)]
inline overload size(a:Reversed[A]) = size(a.seq);

[A when CallDefined?(StaticSize, Static[A])]
overload StaticSize(#Reversed[A]) = StaticSize(A);

[A, I when CallDefined?(index, A, I) and CallDefined?(size, A)]
inline overload index(a:Reversed[A], i:I) =
    forward ..index(a.seq, size(a.seq) - i - 1);
