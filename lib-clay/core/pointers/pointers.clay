import core.coordinates.*;


/// @section  type predicates 

[T]
ContiguousCoordinate?(#Pointer[T]) : Bool = true;



/// @section  null? 

define null?;



/// @section  Pointer - constructor 

[T]
forceinline Pointer[T]() : Pointer[T] = nullPointer(Pointer[T]);



/// @section  Pointer - convert to/from integers and other pointers 

[I,T when Integer?(I)]
forceinline I(a:Pointer[T]) : I = pointerToInt(I, a);

[I,T when Integer?(I)]
forceinline Pointer[T](a:I) : Pointer[T] = intToPointer(Pointer[T], a);

[DEST, SRC]
forceinline Pointer[DEST](a:Pointer[SRC]) : Pointer[DEST] 
    = bitcast(Pointer[DEST], a);



/// @section  Pointer - assign, equals?, lesser? 

[T]
forceinline equals?(a:Pointer[T], b:Pointer[T]) : Bool
    = integerEquals?(a, b);

[T]
forceinline lesser?(a:Pointer[T], b:Pointer[T]) : Bool 
    = integerLesser?(a, b);



/// @section  Pointer - arithmetic 

[T,I when Integer?(I)]
forceinline add(p:Pointer[T], i:I) : Pointer[T] = pointerOffset(p, i);

[T,I when Integer?(I)]
forceinline add(i:I, p:Pointer[T]) : Pointer[T]= pointerOffset(p, i);

[T,I when Integer?(I)]
forceinline subtract(p:Pointer[T], i:I) : Pointer[T] 
    = pointerOffset(p, wrapSubtract(PtrInt(i)));

[T]
forceinline subtract(a:Pointer[T], b:Pointer[T]) : PtrInt
    = wrapQuotient(wrapSubtract(PtrInt(a), PtrInt(b)), PtrInt(TypeSize(T)));

[T]
forceinline inc(ref a:Pointer[T]) : {
    a +: 1;
}

[T]
forceinline dec(ref a:Pointer[T]) : {
    a -: 1;
}



/// @section  Pointer - null, null?, dereference, index 

[T]
forceinline null(#T) = nullPointer(Pointer[T]);

[T]
forceinline null?(x:Pointer[T]) : Bool = x == null(T);

[T]
forceinline dereference(p:Pointer[T]) : ByRef[T] = ref pointerDereference(p);

[T, I when Integer?(I)]
forceinline index(p:Pointer[T], i:I) : ByRef[T] = ref (p + i)^;



/// @section  CodePointer?, ExternalCodePointer? 

define CodePointer?(x) : Bool;
CodePointer?(x) : Bool = false;
[In,Out] CodePointer?(#CodePointer[In,Out]) : Bool = true;

define ExternalCodePointer?(x) : Bool;
ExternalCodePointer?(x) : Bool = false;
[CC,V?,In,Out] ExternalCodePointer?(#ExternalCodePointer[CC,V?,In,Out]) : Bool = true;


/// @section  CodePointer - constructor 

[In, Out]
forceinline CodePointer[In,Out]() = nullPointer(CodePointer[In,Out]);



/// @section  CodePointer - convert to/from other pointers/integers 

[T, In, Out]
forceinline Pointer[T](a:CodePointer[In,Out]) : Pointer[T] = bitcast(Pointer[T], a);

[T, In, Out]
forceinline CodePointer[In,Out](a:Pointer[T]) : CodePointer[In,Out]
    = bitcast(CodePointer[In,Out], a);

[In1, Out1, In2, Out2]
forceinline CodePointer[In1,Out1](a:CodePointer[In2,Out2]) : CodePointer[In1,Out1]
    = bitcast(CodePointer[In1,Out1], a);

[In, Out, I when Integer?(I)]
forceinline CodePointer[In,Out](i:I) : CodePointer[In,Out]
    = intToPointer(CodePointer[In,Out], i);

[In, Out, I when Integer?(I)]
forceinline I(a:CodePointer[In,Out]) : I = pointerToInt(I, a);



//
// CodePointer - equals?, lesser?, null?

[In, Out]
forceinline equals?(a:CodePointer[In,Out], b:CodePointer[In,Out]) : Bool
    = integerEquals?(a, b);

[In, Out]
forceinline lesser?(a:CodePointer[In,Out], b:CodePointer[In,Out]) : Bool
    = integerLesser?(a, b);

[In, Out]
forceinline null?(p:CodePointer[In,Out]) : Bool 
    = p == nullPointer(CodePointer[In,Out]);



/// @section  ExternalCodePointer - constructor 

[CC,V?,I,O]
forceinline ExternalCodePointer[CC,V?,I,O]() =
    nullPointer(ExternalCodePointer[CC,V?,I,O]);



/// @section  ExternalCodePointer - convert to/from other pointers/integers 

[T, CC,V?,I,O]
forceinline Pointer[T](a:ExternalCodePointer[CC,V?,I,O]) : Pointer[T] 
    = bitcast(Pointer[T], a);

[T, CC,V?,I,O]
forceinline ExternalCodePointer[CC,V?,I,O](a:Pointer[T]) 
    : ExternalCodePointer[CC,V?,I,O]
        = bitcast(ExternalCodePointer[CC,V?,I,O], a);

[CC1,V1?,I1,O1, CC2,V2?,I2,O2]
forceinline ExternalCodePointer[CC1,V1?,I1,O1](a:ExternalCodePointer[CC2,V2?,I2,O2]) 
    : ExternalCodePointer[CC1,V1?,I1,O1]
        = bitcast(ExternalCodePointer[CC1,V1?,I1,O1], a);

[CC,V?,In,Out,I when Integer?(I)]
forceinline ExternalCodePointer[CC,V?,In,Out](i:I) : ExternalCodePointer[CC,V?,In,Out]
    = intToPointer(ExternalCodePointer[CC,V?,In,Out], i);

[CC,V?,In,Out,I when Integer?(I)]
forceinline I(a:ExternalCodePointer[CC,V?,In,Out]) : I 
    = pointerToInt(I, a);


/// @section  ExternalCodePointer - equals?, lesser?, null? 

[C,V,I,O]
forceinline equals?(a:ExternalCodePointer[C,V,I,O], b:ExternalCodePointer[C,V,I,O])
    : Bool 
        = integerEquals?(a, b);

[C,V,I,O]
forceinline lesser?(a:ExternalCodePointer[C,V,I,O], b:ExternalCodePointer[C,V,I,O]) 
    : Bool
        = integerLesser?(a, b);

[C,V,I,O]
forceinline null?(p:ExternalCodePointer[C,V,I,O]) : Bool 
    = p == nullPointer(ExternalCodePointer[C,V,I,O]);


/// @section  ExternalCodePointer - call 

private define CCastable?;

[F, T when Type?(F) and Type?(T)]
CCastable?(#F, #T) : Bool = false;

[F, T when Integer?(F) and Integer?(T) and TypeSize(T) >= TypeSize(F)]
CCastable?(#F, #T) : Bool = true;

[F, T when Integer?(F) and Integer?(T) and TypeSize(T) == TypeSize(F)]
CCastable?(#Pointer[F], #Pointer[T]) : Bool = true;

[F]
CCastable?(#Pointer[F], #RawPointer) : Bool = true;

[From, To]
private define cCast(from:From, #To) : To;

[F, T when CCastable?(F, T)]
cCast(from:F, #T) : T = T(from);

[S when Sequence?(S) and (Char == SequenceElementType(S))]
cCast(s:S, #Pointer[CChar]) : Pointer[CChar] = cstring(s);

[F]
cCast(forward from:F, #F) = forward from;

[CC, ..In, ..Out, ..A when countValues(..In) == countValues(..A)]
forceinline call(ptr:ExternalCodePointer[CC, false, [..In], [..Out]], ..args:A) =
    ..callExternalCodePointer(ptr,
        ..mapValues2((arg, T) => cCast(arg, T), #countValues(..A), ..args, ..In));

[CC, ..In, ..Out]
forceinline call(ptr:ExternalCodePointer[CC, false, [..In], [..Out]], ..args:In) =
    ..callExternalCodePointer(ptr, ..args);

[CC, ..In, ..Out, ..A when (countValues(..A) >= countValues(..In))]
forceinline call(ptr:ExternalCodePointer[CC, true, [..In], [..Out]], ..args:A) {
    alias N = #countValues(..In);
    return ..callExternalCodePointer(ptr,
        ..mapValues2((arg, T) => cCast(arg, T), N, ..takeValues(N, ..args), ..In),
        ..dropValues(N, ..args));
}

[CC, ..In, ..Out, ..A when (countValues(..A) >= countValues(..In))
                       and (Tuple[..takeValues(#countValues(..In), ..A)] == Tuple[..In])]
forceinline call(ptr:ExternalCodePointer[CC, true, [..In], [..Out]], ..args:A) =
    ..callExternalCodePointer(ptr, ..args);


/// @section  construct code pointers from #callables 

[..In, ..Out, fn when CallDefined?(fn, ..In)]
forceinline CodePointer[[..In], [..Out]](#fn) : CodePointer[[..In], [..Out]]
    = makeCodePointer(fn, ..In);

[CC, V?, ..In, ..Out, fn when CallDefined?(fn, ..In)]
forceinline ExternalCodePointer[CC, V?, [..In], [..Out]](#fn)
    : ExternalCodePointer[CC, V?, [..In], [..Out]]
    = makeExternalCodePointer(fn, #CC, #V?, ..In);


/// @section  code pointers are Mono? 

[..In, ..Out]
alias MonoInputTypes(f:CodePointer[[..In], [..Out]]) = ..In;
[CC, ..In, ..Out]
alias MonoInputTypes(f:ExternalCodePointer[CC, false, [..In], [..Out]]) = ..In;


/// @section  aliases for external code pointer types 

alias CCodePointer[I,O] = ExternalCodePointer[cdecl, false, I, O];
alias VarArgsCCodePointer[I,O] = ExternalCodePointer[cdecl, true, I, O];
alias StdCallCodePointer[I,O] = ExternalCodePointer[stdcall, false, I, O];
alias FastCallCodePointer[I,O] = ExternalCodePointer[fastcall, false, I, O];
alias ThisCallCodePointer[I,O] = ExternalCodePointer[thiscall, false, I, O];
alias LLVMCodePointer[I,O] = ExternalCodePointer[llvm, false, I, O];

alias makeCCodePointer(fn, ..In) =
    makeExternalCodePointer(fn, cdecl, #false, ..In);


/// @section  construct code pointers from mono #callables 

[fn when Mono?(fn)]
forceinline CodePointer(#fn) = makeCodePointer(fn, ..MonoInputTypes(fn));
[fn when Mono?(fn)]
forceinline CCodePointer(#fn) =
    makeExternalCodePointer(fn, cdecl, #false, ..MonoInputTypes(fn));
[fn when Mono?(fn)]
forceinline StdCallCodePointer(#fn) =
    makeExternalCodePointer(fn, stdcall, #false, ..MonoInputTypes(fn));
[fn when Mono?(fn)]
forceinline FastCallCodePointer(#fn) =
    makeExternalCodePointer(fn, fastcall, #false, ..MonoInputTypes(fn));
[fn when Mono?(fn)]
forceinline ThisCallCodePointer(#fn) =
    makeExternalCodePointer(fn, thiscall, #false, ..MonoInputTypes(fn));
[fn when Mono?(fn)]
forceinline LLVMCodePointer(#fn) =
    makeExternalCodePointer(fn, llvm, #false, ..MonoInputTypes(fn));
