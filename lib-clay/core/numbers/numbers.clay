import core.platform.(OSFamily, Unix, CPU, X86_64);


/// @section  numeric type predicates 

[T]
define SignedInteger?(#T) : Bool;
override SignedInteger?(T) : Bool = false;
override SignedInteger?(#Int8) : Bool = true;
override SignedInteger?(#Int16) : Bool = true;
override SignedInteger?(#Int32) : Bool = true;
override SignedInteger?(#Int64) : Bool = true;
override SignedInteger?(#Int128) : Bool = true;


[T]
define UnsignedInteger?(#T) : Bool;
override UnsignedInteger?(T) : Bool = false;
override UnsignedInteger?(#UInt8) : Bool = true;
override UnsignedInteger?(#UInt16) : Bool = true;
override UnsignedInteger?(#UInt32) : Bool = true;
override UnsignedInteger?(#UInt64) : Bool = true;
override UnsignedInteger?(#UInt128) : Bool = true;

define Integer?(..T) : Bool;
[T] override Integer?(#T) : Bool = SignedInteger?(T) or UnsignedInteger?(T);
[A, B] override Integer?(#A, #B) : Bool = Integer?(A) and Integer?(B);


/// true iff integer operations work on given type
[I when Integer?(I)]
IntegerAvailable?(#I) : Bool =
    // Int128 is available only on x86-64 Unices
    TypeSize(I) <= TypeSize(Int64) or (CPU == X86_64 and OSFamily == Unix);


/// builtin integer types
BuiltinIntegerTypes() = Int8, UInt8, Int16, UInt16, Int32, UInt32, Int64, UInt64, Int128, UInt128;


/// get signed integer type by type size
[S]
define SignedIntegerOfSize(#S);

/// get unsigned integer type by type size
[S]
define UnsignedIntegerOfSize(#S);

override SignedIntegerOfSize(#1) = Int8;
override SignedIntegerOfSize(#2) = Int16;
override SignedIntegerOfSize(#4) = Int32;
override SignedIntegerOfSize(#8) = Int64;
override SignedIntegerOfSize(#16) = Int128;

override UnsignedIntegerOfSize(#1) = UInt8;
override UnsignedIntegerOfSize(#2) = UInt16;
override UnsignedIntegerOfSize(#4) = UInt32;
override UnsignedIntegerOfSize(#8) = UInt64;
override UnsignedIntegerOfSize(#16) = UInt128;


/// for integer type return signed integer of same type
[I when Integer?(I)]
SignedInteger(#I) = SignedIntegerOfSize(#Int(TypeSize(I)));

/// for integer type return unsigned integer of same type
[I when Integer?(I)]
UnsignedInteger(#I) = UnsignedIntegerOfSize(#Int(TypeSize(I)));


[T] define ByteSizedInteger?(#T) : Bool;
override ByteSizedInteger?(T) : Bool = false;
override ByteSizedInteger?(#Int8) : Bool  = true;
override ByteSizedInteger?(#UInt8) : Bool  = true;

define Float?(..T) : Bool;
override Float?(T) : Bool = false;
override Float?(#Float32) : Bool  = true;
override Float?(#Float64) : Bool  = true;
override Float?(#Float80) : Bool  = true;
[A, B] override Float?(#A, #B) : Bool = Float?(A) and Float?(B);


/// builtin float types
BuiltinFloatTypes() = Float32, Float64, Float80;


define Imaginary?(..T) : Bool;
override Imaginary?(T) : Bool = false;
override Imaginary?(#Imag32) : Bool = true;
override Imaginary?(#Imag64) : Bool = true;
override Imaginary?(#Imag80) : Bool = true;
[A, B] override Imaginary?(#A, #B) : Bool = Imaginary?(A) and Imaginary?(B);


/// builtin imaginary types
BuiltinImaginaryTypes() = Imag32, Imag64, Imag80;


define Numeric?(..T) : Bool;
[T] override Numeric?(#T) : Bool = Integer?(T) or Float?(T) or Imaginary?(T);
[A, B] override Numeric?(#A, #B) : Bool = Numeric?(A) and Numeric?(B);



/// @section  BiggerInteger?, BiggerFloat?, BiggerNumeric? 

[A,B when Integer?(A,B)]
private BiggerInteger?(#A, #B) : Bool =
    (TypeSize(A) > TypeSize(B)) or ((TypeSize(A) == TypeSize(B)) and
                                    UnsignedInteger?(A) and
                                    SignedInteger?(B));

private define BiggerFloat?;

[A,B when Float?(A,B) or Imaginary?(A,B)]
override BiggerFloat?(#A, #B) : Bool  = TypeSize(A) > TypeSize(B);

[A,B when (Float?(A) and Imaginary?(B)) or (Float?(B) and Imaginary?(A))]
override BiggerFloat?(#A, #B) : Bool  = TypeSize(A) > TypeSize(B);


private define BiggerNumeric?;

[A,B when (Float?(A) or Imaginary?(A)) and Integer?(B)]
override BiggerNumeric?(#A, #B) : Bool  = true;

[A,B when Integer?(A) and (Float?(B) or Imaginary?(B))]
override BiggerNumeric?(#A, #B) : Bool = false;

[A,B when Integer?(A,B)]
override BiggerNumeric?(#A, #B): Bool = BiggerInteger?(A,B);

[A,B when Float?(A,B) or Imaginary?(A,B)]
override BiggerNumeric?(#A, #B) : Bool = BiggerFloat?(A,B);

[A,B when (Float?(A) and Imaginary?(B)) or (Float?(B) and Imaginary?(A))]
override BiggerNumeric?(#A, #B) : Bool = BiggerFloat?(A,B);




/// @section  toBiggerNumericType 

define toBiggerNumericType;

[A when Numeric?(A)]
forceinline override toBiggerNumericType(#A, #A, v) : A = v;

[A,B when Numeric?(A,B) and BiggerNumeric?(A,B)]
forceinline override toBiggerNumericType(#A, #B, v) : A = numericConvert(A,v);

[A,B when Numeric?(A,B) and BiggerNumeric?(B,A)]
forceinline override toBiggerNumericType(#A, #B, v) : B = numericConvert(B,v);

define wrapToBiggerNumericType;

[A when Numeric?(A)]
forceinline override wrapToBiggerNumericType(#A, #A, v) : A = v;

[A,B when Numeric?(A,B) and BiggerNumeric?(A,B)]
forceinline override wrapToBiggerNumericType(#A, #B, v) : A = wrapCast(A, v);

[A,B when Numeric?(A,B) and BiggerNumeric?(B,A)]
forceinline override wrapToBiggerNumericType(#A, #B, v) : B = wrapCast(B, v);

define ImagBaseType;
override ImagBaseType(#Imag32) = Float;
override ImagBaseType(#Imag64) = Double;
override ImagBaseType(#Imag80) = LongDouble;



/// @section  constructors 

[T when Numeric?(T)]
forceinline override T() : T = numericConvert(T, 0);

[T1,T2 when Numeric?(T1,T2)]
forceinline override T1(src:T2) : T1 = numericConvert(T1, src);

[T,I when Float?(T) and Imaginary?(I)]
forceinline override T(a:I) : T = T(0.);

[T,I when Float?(T) and Imaginary?(I)]
forceinline override I(a:T) : I = I(0.j);



/// @section  assign 

[A, B when Numeric?(A,B) and BiggerNumeric?(A,B)]
forceinline override assign(ref dest:A, src:B) : {
    bitcopy(dest, numericConvert(A,src));
}

[A, B when Integer?(A,B) and (TypeSize(A) == TypeSize(B))]
forceinline override assign(ref dest:A, src:B) : {
    bitcopy(dest, A(src));
}



/// @section  equals?,  notEquals?, lesser?, lesserEquals?, greater?, greaterEquals? 

[A,B when Numeric?(A,B)]
forceinline override (==)(a:A, b:B) : Bool  =
    (==)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Numeric?(A)]
forceinline override (==)(a:A, b:A) : Bool = integerEquals?(a, b);

[A when Float?(A) or Imaginary?(A)]
forceinline override (==)(a:A, b:A) : Bool  = floatOrderedEquals?(a, b);


[A, B when Numeric?(A, B)]
forceinline override (!=)(a:A, b:B) : Bool  =
    (!=)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Numeric?(A)]
forceinline override (!=)(a:A, b:A) : Bool = not (==)(a, b);

[A when Float?(A) or Imaginary?(A)]
forceinline override (!=)(a:A, b:A) : Bool = floatUnorderedNotEquals?(a, b);


[A,B when Numeric?(A,B)]
forceinline override (<)(a:A, b:B) : Bool =
    (<)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Numeric?(A)]
forceinline override (<)(a:A, b:A) : Bool = integerLesser?(a, b);

[A when Float?(A) or Imaginary?(A)]
forceinline override (<)(a:A, b:A) : Bool = floatOrderedLesser?(a, b);


[A, B when Numeric?(A, B)]
forceinline override (<=)(a:A, b:B) : Bool =
    (<=)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Numeric?(A)]
forceinline override (<=)(a:A, b:A) : Bool  = not (<)(b, a);

[A when Float?(A) or Imaginary?(A)]
forceinline override (<=)(a:A, b:A) : Bool = floatOrderedLesserEquals?(a, b);


[A,B when Numeric?(A,B)]
forceinline override (>)(a:A, b:B) : Bool =
    (>)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Numeric?(A)]
forceinline override (>)(a:A, b:A) : Bool = (<)(b, a);

[A when Float?(A) or Imaginary?(A)]
forceinline override (>)(a:A, b:A) : Bool = floatOrderedGreater?(a, b);


[A, B when Numeric?(A, B)]
forceinline override (>=)(a:A, b:B) : Bool =
    (>=)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Numeric?(A)]
forceinline override (>=)(a:A, b:A) : Bool = not (<)(a, b);

[A when Float?(A) or Imaginary?(A)]
forceinline override (>=)(a:A, b:A) : Bool = floatOrderedGreaterEquals?(a, b);

forceinline inRange?(x,l,u) : Bool =  (<)(x,u) and (<=)(l,x);

forceinline inRangeEquals?(x,l,u) : Bool  =  (<=)(x,u) and (<=)(l,x);

define zero?;

[T]
forceinline override zero?(x:T) : Bool = (==)(x, T(0));



/// @section  binary ops 

[A,B when Numeric?(A,B)]
forceinline override (+)(a:A, b:B) =
    (+)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Numeric?(A)]
forceinline override (+)(a:A, b:A) : A = numericAdd(a, b);


[A,B when Numeric?(A,B)]
forceinline override (-)(a:A, b:B) =
    (-)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Numeric?(A)]
forceinline override (-)(a:A, b:A) : A = numericSubtract(a, b);


[A,B when Numeric?(A,B)]
forceinline override (*)(a:A, b:B) =
    (*)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Numeric?(A)]
forceinline override (*)(a:A, b:A) : A = numericMultiply(a, b);


[A,B when Float?(A,B)]
forceinline override (/)(a:A, b:B) =
    (/)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Float?(A)]
forceinline override (/)(a:A, b:A) : A = floatDivide(a, b);

[A,B when Integer?(A,B)]
forceinline override (\)(a:A, b:B) =
    (\)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Integer?(A)]
forceinline override (\)(a:A, b:A) : A = integerQuotient(a, b);

[A,B when Integer?(A,B)]
forceinline override (%)(a:A, b:B) =
    (%)(toBiggerNumericType(A, B, a), toBiggerNumericType(A, B, b));

[A when Integer?(A)]
forceinline override (%)(a:A, b:A) : A = integerRemainder(a, b);

/// @section  Variadic ops 

[A, ..AA when countValues(..AA) != 1]
forceinline override (+)(a:A, ..aa:AA) : A = foldlValues((+), a, ..aa);

[A, ..AA when countValues(..AA) != 1]
forceinline override (-)(a:A, ..aa:AA) : A = foldlValues((-), a, ..aa);

[A, ..AA when countValues(..AA) != 1]
forceinline override (*)(a:A, ..aa:AA) : A = foldlValues((*), a, ..aa);

[A, ..AA when countValues(..AA) != 1]
forceinline override (/)(a:A, ..aa:AA) : A = foldlValues((/), a, ..aa);

[A, ..AA when countValues(..AA) != 1]
forceinline override (\)(a:A, ..aa:AA) : A = foldlValues((\), a, ..aa);

[A, ..AA when countValues(..AA) != 1]
forceinline override (%)(a:A, ..aa:AA) : A = foldlValues((%), a, ..aa);

/// @section  Imaginary binary ops 


[A,B when Numeric?(A) and Imaginary?(B)]
forceinline override (*)(a:A, b:B) = numericMultiply(numericConvert(B,a), b);

[A,B when Numeric?(B) and Imaginary?(A)]
forceinline override (*)(a:A, b:B) = numericMultiply(a, numericConvert(A,b));

[A when Imaginary?(A)]
forceinline override (*)(a:A, b:A) = numericConvert(ImagBaseType(A),(-)(numericMultiply(a, b)));

[A,B when Numeric?(A) and Imaginary?(B)]
forceinline override (/)(a:A, b:B) = (/)(numericConvert(B,-a), b);

[A,B when Numeric?(B) and Imaginary?(A)]
forceinline override (/)(a:A, b:B) = (/)(a, numericConvert(A,b));

[A when Imaginary?(A)]
forceinline override (/)(a:A, b:A) = numericConvert(ImagBaseType(A),floatDivide(a, b));


/// @section  unary ops 

[A when Numeric?(A)]
forceinline override (+)(a:A) : A = a;

[A when Numeric?(A) and not UnsignedInteger?(A)]
forceinline override (-)(a:A) : A = numericNegate(a);



/// @section  inc, dec 

forceinline override inc(ref ..a) : { eachValue(inc, ..a); }
forceinline override dec(ref ..a) : { eachValue(dec, ..a); }

[A when Numeric?(A)]
forceinline override inc(ref a:A) : {
    a +: A(1);
}

[A when Numeric?(A)]
forceinline override dec(ref a:A) : {
    a -: A(1);
}



/// @section  bitwise ops 

[A,B when Integer?(A,B)]
forceinline override (<<)(a:A, b:B) : A = integerShiftLeft(a, A(b));

[A,B when Integer?(A,B)]
forceinline override (>>)(a:A, b:B) : A = integerShiftRight(a, A(b));

[A,B when Integer?(A,B)]
forceinline override (&)(a:A, b:B) =
    integerBitwiseAnd(wrapToBiggerNumericType(A, B, a),
                      wrapToBiggerNumericType(A, B, b));

[A when Integer?(A)]
forceinline override (&)(a:A, b:A) : A = integerBitwiseAnd(a, b);

[A,B when Integer?(A,B)]
forceinline override (|)(a:A, b:B) =
    integerBitwiseOr(wrapToBiggerNumericType(A, B, a),
                     wrapToBiggerNumericType(A, B, b));

[A when Integer?(A)]
forceinline override (|)(a:A, b:A) : A = integerBitwiseOr(a, b);

[A,B when Integer?(A,B)]
forceinline override (~)(a:A, b:B) =
    integerBitwiseXor(wrapToBiggerNumericType(A, B, a),
                      wrapToBiggerNumericType(A, B, b));

[A when Integer?(A)]
forceinline override (~)(a:A, b:A) : A = integerBitwiseXor(a, b);

[A when Integer?(A)]
forceinline override (~)(a:A) : A = integerBitwiseNot(a);



//
// octal()

[T,I when Integer?(T) and StringLiteral?(I)]
private _octal(#T, #I)
    = #foldlValues((n,d) -> T(8)*T(n) + T(d - 0x30), T(0), ..stringLiteralBytes(I));

define octal;
override octal(T, I) = unwrapStatic(_octal(T, I));
override octal(I) = octal(Int, I);


/// @section  overflow-checked math 

IntegerOverflowChecks?() = AllAssertionsEnabled?()
    and AssertionEnabled?("overflowChecks");

[A when Integer?(A) and IntegerOverflowChecks?()]
forceinline override (+)(a:A, b:A) : A
    = integerAddChecked(a, b);
[A when Integer?(A) and IntegerOverflowChecks?()]
forceinline override (-)(a:A, b:A) : A
    = integerSubtractChecked(a, b);
[A when Integer?(A) and IntegerOverflowChecks?()]
forceinline override (*)(a:A, b:A) : A
    = integerMultiplyChecked(a, b);
[A when Integer?(A) and IntegerOverflowChecks?()]
forceinline override (\)(a:A, b:A) : A
    = integerQuotientChecked(a, b);
[A when Integer?(A) and IntegerOverflowChecks?()]
forceinline override (%)(a:A, b:A) : A
    = integerRemainderChecked(a, b);

[A, B when Integer?(A, B) and IntegerOverflowChecks?()]
forceinline override bitshl(a:A, b:B) : A
    = integerShiftLeftChecked(a, A(b));

[A when SignedInteger?(A) and IntegerOverflowChecks?()]
forceinline override (-)(a:A) : A
    = integerNegateChecked(a);

[A, B when Integer?(A) and Numeric?(B) and IntegerOverflowChecks?()]
forceinline override A(b:B) : A
    = integerConvertChecked(A, b);


/// @section  never-checked wrap-on-overflow math 

define wrapAdd;
define wrapNegate;
define wrapSubtract;
define wrapMultiply;
define wrapQuotient;
define wrapRemainder;

[A when Integer?(A)]
forceinline override wrapNegate(a:A) : A = numericNegate(a);
[A when Integer?(A)]
forceinline override wrapAdd(a:A, b:A) : A = numericAdd(a, b);
[A when Integer?(A)]
forceinline override wrapSubtract(a:A, b:A) : A = numericSubtract(a, b);
[A when Integer?(A)]
forceinline override wrapMultiply(a:A, b:A) : A = numericMultiply(a, b);
[A when Integer?(A)]
forceinline override wrapQuotient(a:A, b:A) : A =  integerQuotient(a, b);
[A when Integer?(A)]
forceinline override wrapRemainder(a:A, b:A) : A = integerRemainder(a, b);

[A, ..AA when equalValues?(A,..AA) and countValues(..AA) != 1]
forceinline override wrapAdd(a:A, ..aa:AA) : A = foldValues(wrapAdd, a, ..aa);

[A, ..AA when equalValues?(A,..AA) and countValues(..AA) != 1]
forceinline override wrapSubtract(a:A, ..aa:AA) : A = foldlValues(wrapSubtract, a, ..aa);

[A, ..AA when equalValues?(A,..AA) and countValues(..AA) != 1]
forceinline override wrapMultiply(a:A, ..aa:AA) : A = foldValues(wrapMultiply, a, ..aa);

[A, ..AA when equalValues?(A,..AA) and countValues(..AA) != 1]
forceinline override wrapQuotient(a:A, ..aa:AA) : A = foldlValues(wrapQuotient, a, ..aa);

[A, ..AA when equalValues?(A,..AA) and countValues(..AA) != 1]
forceinline override wrapRemainder(a:A, ..aa:AA) : A = foldlValues(wrapRemainder, a, ..aa);

[A, B when Integer?(A, B)]
forceinline wrapBitshl(a:A, b:B) : A = integerShiftLeft(a, A(b));

[A when SignedInteger?(A)]
forceinline override wrapSubtract(a:A) : A = numericNegate(a);

[A, B when Integer?(A) and Numeric?(B)]
forceinline wrapCast(#A, b:B) : A = numericConvert(A, b);
