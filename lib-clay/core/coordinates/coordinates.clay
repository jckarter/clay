
/// @section  coordinate type predicates 

Coordinate?(T) : Bool = CallDefined?(dereference, T);
CoordinateTargetType(T) = ..Type(..dereference(typeToLValue(T)));

RandomAccessCoordinate?(T) : Bool =
    Coordinate?(T) and
    CallDefined?((+), T, Int) and
    CallDefined?((-), T, Int) and
    CallDefined?((-), T, T);

LValueCoordinate?(T) =
    Coordinate?(T) and
    LValue?(dereference(typeToLValue(T)));

// true if adjacent coordinates point to adjacent locations in memory
[T]
define ContiguousCoordinate?(#T) : Bool;

overload ContiguousCoordinate?(T) : Bool = false;



/// @section  ValueCoordinate 

record ValueCoordinate[T] (hasValue? : Bool, value : T);
[T] overload ValueCoordinate[T](value:T) = ValueCoordinate[T](true, value);
[T] overload ValueCoordinate(value:T) = ValueCoordinate[T](true, value);

[T]
forceinline overload dereference(c:ValueCoordinate[T]) : T = c.value;

[T]
forceinline overload getValue(c:ValueCoordinate[T]) : T = c.value;
[T]
forceinline overload hasValue?(c:ValueCoordinate[T]) : Bool = c.hasValue?;

[T,I when CallDefined?(add, T, I)]
forceinline overload add(c:ValueCoordinate[T], i:I) =
    ValueCoordinate(T(c.value + i));

[T,I when CallDefined?(subtract, T, I)]
forceinline overload subtract(c:ValueCoordinate[T], i:I) =
    ValueCoordinate(T(c.value - i));

[T when CallDefined?(subtract, T, T)]
forceinline overload subtract(c1:ValueCoordinate[T], c2:ValueCoordinate[T]) : T =
    c1.value - c2.value;

[T]
forceinline overload inc(ref c:ValueCoordinate[T]) : {
    inc(c.value);
}

[T]
forceinline overload dec(ref c:ValueCoordinate[T]) : {
    dec(c.value);
}



/// @section  ValueReverseCoordinate 

record ValueReverseCoordinate[T] (value : T);

[T]
forceinline overload dereference(c:ValueReverseCoordinate[T]) : T = (c.value - 1);

[T,I when CallDefined?(subtract, T, I)]
forceinline overload add(c:ValueReverseCoordinate[T], i:I) =
    ValueReverseCoordinate(T(c.value - i));

[T,I when CallDefined?(add, T, I)]
forceinline overload subtract(c:ValueReverseCoordinate[T], i:I) =
    ValueReverseCoordinate(T(c.value + i));

[T when CallDefined?(subtract, T, T)]
forceinline overload subtract(c1:ValueReverseCoordinate[T], c2:ValueReverseCoordinate[T]) : T =
    c2.value - c1.value;

[T]
forceinline overload inc(ref c:ValueReverseCoordinate[T]) : {
    dec(c.value);
}

[T]
forceinline overload dec(ref c:ValueReverseCoordinate[T]) : {
    inc(c.value);
}



/// @section  ReverseCoordinate 

record ReverseCoordinate[T] (coord : T);

[T]
forceinline overload dereference(c:ReverseCoordinate[T]) = forward ..(c.coord - 1)^;

[T]
forceinline overload getValue(c:ReverseCoordinate[T]) : T = forward ..getValue(c.coord - 1);
[T]
forceinline overload hasValue?(c:ReverseCoordinate[T]) : Bool = hasValue?(c.coord);

[T,I when CallDefined?(subtract, T, I)]
forceinline overload add(c:ReverseCoordinate[T], i:I) =
    ReverseCoordinate(T(c.coord - i));

[T,I when CallDefined?(add, T, I)]
forceinline overload subtract(c:ReverseCoordinate[T], i:I) =
    ReverseCoordinate(T(c.coord + i));

[T when CallDefined?(subtract, T, T)]
forceinline overload subtract(c1:ReverseCoordinate[T], c2:ReverseCoordinate[T]) : T =
    c2.coord - c1.coord;

[T]
forceinline overload inc(ref c:ReverseCoordinate[T]) : {
    dec(c.coord);
}

[T]
forceinline overload dec(ref c:ReverseCoordinate[T]) : {
    inc(c.coord);
}
