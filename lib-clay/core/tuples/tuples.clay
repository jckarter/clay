import __operators__;


/// @section  Tuple? 

Tuple?(x) = false;
[..T] overload Tuple?(#Tuple[..T]) = true;



/// @section  TupleElements 

[..T] TupleElements(#Tuple[..T]) = ..T;



/// @section  staticIndex 

[..T, i]
inline overload staticIndex(a:Tuple[..T], #i) =
    ref tupleRef(a, #SizeT(i));



/// @section  unpackTuple, unpackTupleRef 

inline unpackTuple(x) = ..unpackTupleRef(x);

[..T]
inline unpackTupleRef(x:Tuple[..T]) = ref ..tupleElements(x);



/// @section  unpack, unpackRef 

inline unpackRef(x) = ref x;
[..T]
inline overload unpackRef(x:Tuple[..T]) = ref ..unpackTupleRef(x);

inline unpack(x) = ..unpackRef(x);



/// @section  Tuple type inferring constructor 

[..T]
alias overload Tuple(..args:T) = Tuple[..T](..args);



/// @section  basic constructor 

[..T]
alias overload Tuple[..T](..args:T) --> returned:Tuple[..T] {
    ..unpackTupleRef(returned) <-- ..args;
}



/// @section  default constructor 

[..T]
inline overload Tuple[..T]() --> returned:Tuple[..T] {
    initializeMulti(..tupleElements(returned));
}

[..A]
private inline initializeMulti(..args:A) {
    ..args <-- ..initializeTypes(..A);
}

private define initializeTypes;
inline overload initializeTypes(A, ..B) = A(), ..initializeTypes(..B);
inline overload initializeTypes() = ;



/// @section  copy constructor 

[..T when not allValues?(BitwiseCopiedType?, ..T)]
inline overload Tuple[..T](src:Tuple[..T]) --> returned:Tuple[..T] {
    ..tupleElements(returned) <-- ..tupleElements(src);
}



/// @section  moveUnsafe, resetUnsafe 

[..T when not allValues?(BitwiseMovedType?, ..T)]
inline overload moveUnsafe(src:Tuple[..T]) --> returned:Tuple[..T] {
    alias n = TupleElementCount(Tuple[..T]);
    ..for (i in staticIntegers(#n))
        staticIndex(returned, i) <-- moveUnsafe(staticIndex(src, i));
}

[..T when not allValues?(NotResetType?, ..T)]
inline overload resetUnsafe(a:Tuple[..T]) {
    ..for (x in tupleElements(a))
        resetUnsafe(x);
}



/// @section  destroy 

[..T when not allValues?(NotDestroyedType?, ..T)]
inline overload destroy(a:Tuple[..T]) {
    ..for (x in tupleElements(a))
        destroy(x);
}



/// @section  assign 

[..T when not allValues?(BitwiseAssignedType?, ..T)]
inline overload assign(ref dest:Tuple[..T], ref src:Tuple[..T]) {
    alias n = TupleElementCount(Tuple[..T]);
    ..for (i in staticIntegers(#n))
        assign(staticIndex(dest, i), staticIndex(src, i));
}



/// @section  equals? 

[..T]
inline overload equals?(a:Tuple[..T], b:Tuple[..T]) {
    alias n = TupleElementCount(Tuple[..T]);
    ..for (i in staticIntegers(#n)) {
        if (not equals?(staticIndex(a, i), staticIndex(b, i)))
            return false;
    }
    return true;
}



/// @section  lesser? 

[..T]
inline overload lesser?(a:Tuple[..T], b:Tuple[..T]) {
    alias n = TupleElementCount(Tuple[..T]);
    ..for (i in staticIntegers(#n)) {
        ref av = staticIndex(a, i);
        ref bv = staticIndex(b, i);
        if (lesser?(av, bv))
            return true;
        if (lesser?(bv, av))
            return false;
    }
    return false;
}



/// @section  tupleLiteral 

alias overload __operators__.tupleLiteral(..args) = Tuple(..args);
