import core.libc.(strlen);


/// @section  CStringRef - null terminated string 

record CStringRef (ptr : Pointer[Int8]);



/// @section  predicates 

ContiguousSequence?(#CStringRef) : Bool = true;



/// @section  constructors 

[T when ByteLike?(T)]
forceinline CStringRef(ptr : Pointer[T]) --> returned:CStringRef {
    returned.ptr = Pointer[Int8](ptr);
}

[S when StringLiteral?(S)]
forceinline CStringRef(#S) = CStringRef(cstring(S));



/// @section  size, index 

forceinline size(a:CStringRef) : SizeT = SizeT(strlen(a.ptr));

[I when Integer?(I)]
forceinline index(a:CStringRef, i:I) {
    assert["boundsChecks"](i >= 0 and i < size(a), "CStringRef index out of bounds");
    return ref (begin(a) + i)^;
}



/// @section  coordinates and iteration 

forceinline begin(a:CStringRef) = Pointer[Char](a.ptr);
forceinline end(a:CStringRef) = Pointer[Char](a.ptr + size(a));

forceinline iterator(a:CStringRef) = coordinateRange(begin(a), end(a));

forceinline reverseIterator(a:CStringRef) =
    reverseCoordinateRange(end(a), begin(a));



/// @section  cstring 

forceinline cstring(a:CStringRef) = a.ptr;
