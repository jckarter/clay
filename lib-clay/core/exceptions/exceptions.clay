import core.libc as libc;
import core.errors.backtrace.*;
import __operators__.(
    throwValue,
    continueException,
    exceptionIs?,
    exceptionAs,
    exceptionAsAny,
    unhandledExceptionInExternal,
    exceptionInInitializer,
    exceptionInFinalizer,
);


/// @section  Exception, NoException, currentException 

variant Exception (NoException);

record NoException ();

overload Exception() = Exception(NoException());

private record ExceptionData (
    exception: Exception,
    backtrace: Backtrace,
    file: StringLiteralRef,
    line: UInt,
);

private exceptionObject(exp:RawPointer) = ref Pointer[ExceptionData](exp)^;



/// @section  printUnhandledExceptionToStderr

// Print unhandled exception to stderr.
// This function is called from program bootstrap code (see
// `core.system.runMain`). Must print at least one line. Last line must
// be terminated with \n character. This is very simple implementation
// that depends on nothing but core. `printer.exception` module contains
// more sophisticated version.
printUnhandledExceptionToStderr(e) : {
    libc.fprintf(libc.stderr, cstring("unhandled %s exception\n"), cstring(MemberTypeName(e)));
}



/// @section  throwValue, continueException 

[T when Type?(T)]
overload throwValue(x:T) : {
    errorWithPrintfNoThrow(stringLiteralConcat("exception of type ", StaticName(T)," thrown with exceptions disabled"));
}

private allocateExceptionData(forward x, file, line) = allocateObject(ExceptionData(Exception(x), Backtrace(), file, line));

[ when ExceptionsEnabled?]
alias overload throwValue(x) : {
    // allocateExceptionData cannot be inlined, because allocateExceptionData is also an alias function that throws
    var exp = allocateExceptionData(x, StringLiteralRef(__FILE__), UInt(__LINE__));
    if (not Flag?("clay.DisableBacktraceOnThrow")) {
        exp^.backtrace = captureBacktrace();
    }
    signalException(RawPointer(exp));
}

overload continueException(exp:RawPointer) : {
    signalException(exp);
}

private signalException(exp:RawPointer) __llvm__{
    %expv = load i8** %exp
    ret i8* %expv
}



/// @section  throwButPropagate 

throwButPropagate(ex, ..T) {
    if (true)
        throw ex;
    else
        return forward ..mapValues(typeToLValue, ..T);
}

alias throwButPropagateLike(ex, ..expr) {
    if (true)
        throw ex;
    else
        return forward ..expr;
}



/// @section  exceptionIs?, exceptionAs, exceptionAsAny 

[T]
overload exceptionIs?(#T, exp:RawPointer) : Bool = nestedVariantIs?(exceptionObject(exp).exception, T);

[T]
overload exceptionAs(#T, exp:RawPointer) : T, Backtrace, StringLiteralRef, UInt {
    finally freeRawMemory(exp);
    return
        moveUnsafe(nestedVariantAs(exceptionObject(exp).exception, T)),
        moveUnsafe(exceptionObject(exp).backtrace),
        exceptionObject(exp).file,
        exceptionObject(exp).line;
}

overload exceptionAsAny(exp:RawPointer) : Exception, Backtrace, StringLiteralRef, UInt {
    finally freeRawMemory(exp);
    return
        moveUnsafe(exceptionObject(exp).exception),
        moveUnsafe(exceptionObject(exp).backtrace),
        exceptionObject(exp).file,
        exceptionObject(exp).line;
}



/// @section  unhandledExceptionInExternal 
//

private forceinline errorMsg(msg) : {
    libc.fprintf(libc.stderr, cstring(msg));
    showBacktrace();
}

overload unhandledExceptionInExternal() : {
    errorMsg("unhandled exception in external procedure\n");
    libc.abort();
}

overload exceptionInInitializer() : {
    errorMsg("exception when initializing globals\n");
    libc.abort();
}

overload exceptionInFinalizer() : {
    errorMsg("exception when destroying globals\n");
    libc.abort();
}
