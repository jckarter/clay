
/// @section  copy*Memory*, move*Memory*, *assign*Memory* for POD types 

[P]
private forceinline copyPODs(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    memmove(destBegin, srcBegin, SizeT(RawPointer(srcEnd)-RawPointer(srcBegin)));
}
[P]
private forceinline copyNonovPODs(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    memcpy(destBegin, srcBegin, SizeT(RawPointer(srcEnd)-RawPointer(srcBegin)));
}

[P when BitwiseCopiedType?(P)]
forceinline override copyNonoverlappingMemory(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    copyNonovPODs(destBegin, srcBegin, srcEnd);
}
[P when BitwiseMovedType?(P)]
forceinline override moveMemory(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    copyPODs(destBegin, srcBegin, srcEnd);
    var destEnd = destBegin + (srcEnd - srcBegin);
    if (destBegin <= srcBegin)
        resetMemoryUnsafe(max(destEnd, srcBegin), srcEnd);
    else
        resetMemoryUnsafe(srcBegin, min(srcEnd, destBegin));
}
[P when BitwiseMovedType?(P) and ResetDoesNothingType?(P)]
forceinline override moveMemory(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    copyPODs(destBegin, srcBegin, srcEnd);
}
[P when BitwiseMovedType?(P)]
forceinline override moveMemoryUnsafe(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    copyPODs(destBegin, srcBegin, srcEnd);
}
[P when BitwiseMovedType?(P)]
forceinline override moveNonoverlappingMemory(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    copyNonovPODs(destBegin, srcBegin, srcEnd);
    resetMemoryUnsafe(srcBegin, srcEnd);
}
[P when BitwiseMovedType?(P) and ResetDoesNothingType?(P)]
forceinline override moveNonoverlappingMemory(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    copyNonovPODs(destBegin, srcBegin, srcEnd);
}
[P when BitwiseMovedType?(P)]
forceinline override moveNonoverlappingMemoryUnsafe(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    copyNonovPODs(destBegin, srcBegin, srcEnd);
}
[P when BitwiseAssignedType?(P)]
forceinline override assignNonoverlappingMemory(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    copyNonovPODs(destBegin, srcBegin, srcEnd);
}
[P when BitwiseMoveAssignedType?(P)]
forceinline override moveAssignNonoverlappingMemory(destBegin:Pointer[P], srcBegin:Pointer[P], srcEnd:Pointer[P]) : {
    copyNonovPODs(destBegin, srcBegin, srcEnd);
}


/// @section  resetMemoryUnsafe, destroyMemory for POD 

[P when ResetDoesNothingType?(P)]
forceinline override resetMemoryUnsafe(begin:Pointer[P], end:Pointer[P]) : { }

[P when DestroyDoesNothingType?(P)]
forceinline override destroyMemory(begin:Pointer[P], end:Pointer[P]) : { }


/// @section  value semantics for POD 

define copyPOD;

[P] forceinline override copyPOD(x: P) --> returned: P { memcpy(@returned, @x, TypeSize(P)); }
forceinline override copyPOD(x: Bool) --> returned: Bool { bitcopy(returned, x); }
[N when Numeric?(N)]
forceinline override copyPOD(x: N) --> returned: N { bitcopy(returned, x); }
[E when Enum?(E)]
forceinline override copyPOD(x: E) --> returned: E { bitcopy(returned, x); }
[T]
forceinline override copyPOD(x: Pointer[T]) --> returned: Pointer[T] { bitcopy(returned, x); }
[In, Out]
forceinline override copyPOD(x: CodePointer[In, Out]) --> returned: CodePointer[In, Out]
    { bitcopy(returned, x); }
[C,V,I,O]
forceinline override copyPOD(x: ExternalCodePointer[C,V,I,O])
    --> returned: ExternalCodePointer[C,V,I,O]
    { bitcopy(returned, x); }

[P when DestroyDoesNothingType?(P)]
forceinline override destroy(x: P) : {}
[P when BitwiseMovedType?(P)]
forceinline override moveUnsafe(x: P) : P = copyPOD(x);
[P when ResetDoesNothingType?(P)]
forceinline override resetUnsafe(x: P) : { }
[P when BitwiseCopiedType?(P)]
forceinline override P(x: P) --> returned: P = copyPOD(x);
[P when BitwiseAssignedType?(P)]
forceinline override assign(ref x: P, y: P) : { x <-- copyPOD(y); }

