/// @module vector.generic
/// generic vector interface

/// @section generic vector requirements

define Vector?;
overload Vector?(x) = false;

define vectorData;
define vectorSize;
define vectorSetSize;
define vectorCapacity;
define vectorRequestCapacity;


/// @section predicates

[V when Vector?(V)]
overload ContiguousSequence?(#V) = true;


/// @section size, index

[V when Vector?(V)]
inline overload size(a:V) = vectorSize(a);

[V,I when Vector?(V) and Integer?(I)]
inline overload index(a:V, i:I) {
    assert["boundsChecks"](i >= 0 and i < size(a),
        V, " index out of bounds");
    return ref (vectorData(a) + SizeT(i))^;
}


/// @section vector coordinates

alias VectorCoordinate[T] = Pointer[T];

[V when Vector?(V)]
inline overload begin(a:V) = vectorData(a);

[V when Vector?(V)]
inline overload end(a:V) = vectorData(a) + size(a);


/// @section vector iteration

/// @overload iterator
/// constructs an iterator on the vector
/// \a ranges.CoordinateRange
[V when Vector?(V)]
inline overload iterator(a:V) = coordinateRange(begin(a), end(a));

/// @overload reverseIterator
/// constructs a reverse iterator on the vector
/// \a ranges.ReverseCoordinateRange
[V when Vector?(V)]
inline overload reverseIterator(a:V) =
    reverseCoordinateRange(end(a), begin(a));


/// @section front, back, push, pop

[V when Vector?(V)]
inline overload front(a:V) = ref a[0];

[V when Vector?(V)]
inline overload back(a:V) = ref a[size(a) - 1];

[V, S when Vector?(V) and Sequence?(S) and (SequenceElementType(S) == SequenceElementType(V))]
overload push(a:V, forward seq:S) {
    insert(a, size(a), seq);
}

[V, T when Vector?(V) and (SequenceElementType(V) == T)]
overload push(a:V, forward x:T) {
    insert(a, size(a), x);
}

[V, A1, A2, ..AA when Vector?(V)]
inline overload push(v:V, forward e1:A1, forward e2:A2, forward ..ee:AA) {
    reserve(v, countValues(A1, A2, ..AA));
    ..for (x in e1, e2, ..ee) {
        push(v, x);
    }
}

[V when Vector?(V)]
inline overload push(v:V) { }

[V when Vector?(V)]
overload pop(a:V) {
    assert(not empty?(a), "pop of empty ", V);
    var temp = moveUnsafe(back(a));
    removeLocations(a, end(a)-1, end(a));
    return move(temp);
}


/// @section reserve, resize, resizeUnsafe, ensureSpace

[V, I when Vector?(V) and Integer?(I)]
overload reserve(a:V, n:I) {
    if (vectorCapacity(a) < SizeT(n))
        vectorRequestCapacity(a, SizeT(n));
}

[V, I when Vector?(V) and Integer?(I)]
inline overload resize(a:V, n:I) {
    resizeInternal(a, SizeT(n), initializeMemory);
}

[V, I when Vector?(V) and Integer?(I)]
inline overload resizeUnsafe(a:V, n:I) {
    resizeInternal(a, SizeT(n), resetMemoryUnsafe);
}

[V when Vector?(V)]
private resizeInternal(a:V, n:SizeT, initializer) {
    var currentSize = vectorSize(a);
    if (currentSize < n) {
        reserve(a, n);
        initializer(end(a), begin(a)+n);
    }
    else if (currentSize > n) {
        destroyMemory(begin(a)+n, end(a));
    }
    vectorSetSize(a, n);
}

private inline ensureSpace(a, space:SizeT) {
    reserve(a, size(a) + space);
}


/// @section insert

[I, J when Integer?(I) and Integer?(J)]
private insertLocationsUnsafe(a, pos:I, n:J) {
    return ..insertLocationsUnsafe(a, SizeT(pos), SizeT(n));
}

overload insertLocationsUnsafe(a, pos:SizeT, n:SizeT) {
    ensureSpace(a, n);
    var first = begin(a) + pos;
    moveMemoryUnsafe(first+n, first, end(a));
    vectorSetSize(a, vectorSize(a) + n);
    return first, first+n;
}

[V,I,S when Vector?(V) and Integer?(I) and Sequence?(S) and
         (SequenceElementType(S) == SequenceElementType(V))]
overload insert(a:V, i:I, seq:S) {
    assert(i >= 0 and i <= size(a), "invalid position for insert into ", V);
    var pos = SizeT(i);
    for (x in seq) {
        insert(a, pos, x);
        pos +: 1;
    }
}

[V,I,S when Vector?(V) and Integer?(I) and SizedSequence?(S) and
         (SequenceElementType(S) == SequenceElementType(V))]
overload insert(a:V, i:I, seq:S) {
    assert(i >= 0 and i <= size(a), "invalid position for insert into ", V);
    var first, last = ..insertLocationsUnsafe(a, i, size(seq));
    try {
        for (x in seq) {
            first^ <-- x;
            inc(first);
        }
    }
    catch (e) {
        resetMemoryUnsafe(first, last);
        throw e;
    }
}

[V,I,S when Vector?(V) and Integer?(I)
         and SizedSequence?(S) and ContiguousSequence?(S)
         and (SequenceElementType(S) == SequenceElementType(V))]
overload insert(a:V, i:I, seq:S) {
    assert(i >= 0 and i <= size(a), "invalid position for insert into ", V);
    var first, last = ..insertLocationsUnsafe(a, i, size(seq));
    try {
        var src = begin(seq);
        copyNonoverlappingMemory(first, src, src+size(seq));
    }
    catch (e) {
        resetMemoryUnsafe(first, last);
        throw e;
    }
}

[V,I,S when Vector?(V) and Integer?(I)
         and SequenceContainer?(S) and SizedSequence?(S)
         and (SequenceElementType(S) == SequenceElementType(V))]
overload insert(a:V, i:I, rvalue seq:S) {
    assert(i >= 0 and i <= size(a), "invalid position for insert into ", V);
    var first, last = ..insertLocationsUnsafe(a, i, size(seq));
    for (x in seq) {
        first^ <-- move(x);
        inc(first);
    }
}

[V,I,S when Vector?(V) and Integer?(I)
         and SequenceContainer?(S) and ContiguousSequence?(S)
         and (SequenceElementType(S) == SequenceElementType(V))]
overload insert(a:V, i:I, rvalue seq:S) {
    assert(i >= 0 and i <= size(a), "invalid position for insert into ", V);
    var first, last = ..insertLocationsUnsafe(a, i, size(seq));
    var src = begin(seq);
    moveNonoverlappingMemory(first, src, src+size(seq));
}

[V,I,T when Vector?(V) and Integer?(I) and (SequenceElementType(V) == T)]
overload insert(a:V, i:I, ref x:T) {
    // NOTE: when 'x' is an lvalue, it _may_ be pointing inside 'a'.
    // so make a copy and insert it as an rvalue.
    insert(a, i, copy(x));
}

[V,I,T when Vector?(V) and Integer?(I) and (SequenceElementType(V) == T)]
overload insert(a:V, i:I, rvalue x:T) {
    var first, last = ..insertLocationsUnsafe(a, i, 1);
    first^ <-- move(x);
}

[V,T,S when Vector?(V) and (SequenceElementType(V) == T) and
         Sequence?(S) and (SequenceElementType(S) == T)]
overload insert(a:V, i:VectorCoordinate[T], forward seq:S) {
    insert(a, i-begin(a), seq);
}

[V,T when Vector?(V) and (SequenceElementType(V) == T)]
overload insert(a:V, i:VectorCoordinate[T], forward x:T) {
    insert(a, i-begin(a), x);
}


/// @section clear, remove

/// @overload clear
/// remove all items from the vector
[V when Vector?(V)]
inline overload clear(a:V) {
    remove(a, begin(a), end(a));
}

[V,T when Vector?(V) and SequenceElementType(V) == T]
private validVectorCoordinate?(v:V, i:VectorCoordinate[T])
    = i >= begin(v) and i <= end(v);

[V,T when Vector?(V) and (SequenceElementType(V) == T)]
private removeLocations(a:V, i:VectorCoordinate[T], j:VectorCoordinate[T]) {
    assert["boundsChecks"](validVectorCoordinate?(a, i),
        "invalid first coordinate for remove from ", V);
    assert["boundsChecks"](validVectorCoordinate?(a, j),
        "invalid last coordinate for remove from ", V);
    var n = end(a) - j;
    moveMemoryUnsafe(i, j, end(a));
    vectorSetSize(a, vectorSize(a) - (j-i));
}

/// @overload remove
/// remove all items from the vector within the range \p i to \a j
[V,T when Vector?(V) and (SequenceElementType(V) == T)]
overload remove(a:V, i:VectorCoordinate[T], j:VectorCoordinate[T]) {
    assert["boundsChecks"](validVectorCoordinate?(a, i),
        "invalid first coordinate for remove from ", V);
    assert["boundsChecks"](validVectorCoordinate?(a, j),
        "invalid last coordinate for remove from ", V);
    destroyMemory(i, j);
    removeLocations(a, i, j);
}

[V,T when Vector?(V) and (SequenceElementType(V) == T)]
overload remove(a:V, i:VectorCoordinate[T]) {
    remove(a, i, i+1);
}

[V,I when Vector?(V) and Integer?(I)]
overload remove(a:V, i:I) {
    remove(a, begin(a)+i);
}

[V,I,J when Vector?(V) and Integer?(I) and Integer?(J)]
overload remove(a:V, i:I, j:J) {
    remove(a, begin(a)+i, begin(a)+j);
}
