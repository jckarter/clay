import byteorder.*;

public import crypto.digest.*;
import crypto.digest.internal.*;



// MD5

alias MD5_NAME = #"MD5";

overload Algorithm?(#MD5_NAME) = true;

overload DigestSize(#MD5_NAME) = 16u;

overload Word(#MD5_NAME) = UInt32;

overload StateSizeWords(#MD5_NAME) = 4;

overload BlockSizeWords(#MD5_NAME) = 16;

record MD5 = MakeDigest(#MD5_NAME);
record MD5Context = MakeDigestContext(#MD5_NAME);
overload AlgorithmTypes(#MD5_NAME) = #MD5, #MD5Context;

var md5Initial = array(
        0x67452301_u,
        0xefcdab89_u,
        0x98badcfe_u,
        0x10325476_u,
    );

overload stateInitial(#MD5_NAME) = ref md5Initial;

// rotation constants
private alias S11 =  7;
private alias S12 = 12;
private alias S13 = 17;
private alias S14 = 22;
private alias S21 =  5;
private alias S22 =  9;
private alias S23 = 14;
private alias S24 = 20;
private alias S31 =  4;
private alias S32 = 11;
private alias S33 = 16;
private alias S34 = 23;
private alias S41 =  6;
private alias S42 = 10;
private alias S43 = 15;
private alias S44 = 21;

// MD5 functions
private FF(a, b, c, d, x, s, ac) {
    var a1 = wrapAdd(a, bitor(bitand(b, c), bitandc(d, b)), x, ac);
    return wrapAdd(bitrol(a1, s), b);
}

private GG(a, b, c, d, x, s, ac) {
    var a1 = wrapAdd(a, bitor(bitand(b, d), bitandc(c, d)), x, ac);
    return wrapAdd(bitrol(a1, s), b);
}

private HH(a, b, c, d, x, s, ac) {
    var a1 = wrapAdd(a, bitxor(b, c, d), x, ac);
    return wrapAdd(bitrol(a1, s), b);
}

private II(a, b, c, d, x, s, ac) {
    var a1 = wrapAdd(a, bitxor(c, bitor(b, bitnot(d))), x, ac);
    return wrapAdd(bitrol(a1, s), b);
}

private alias MD5State = State(#MD5_NAME);
private alias MD5BlockBytes = BlockBytes(#MD5_NAME);

overload processBlock(#MD5_NAME, state: MD5State, chunkBytes: MD5BlockBytes) {
    ref x0 = Pointer[Array[UInt32, 16]](begin(chunkBytes))^;

    var x = bytesToWordsLittle(UInt32, chunkBytes);

    var a = state[0];
    var b = state[1];
    var c = state[2];
    var d = state[3];

    /* Round 1 */
    a = FF(a, b, c, d, x[ 0], S11, 0xd76aa478_u); /*  1 */
    d = FF(d, a, b, c, x[ 1], S12, 0xe8c7b756_u); /*  2 */
    c = FF(c, d, a, b, x[ 2], S13, 0x242070db_u); /*  3 */
    b = FF(b, c, d, a, x[ 3], S14, 0xc1bdceee_u); /*  4 */
    a = FF(a, b, c, d, x[ 4], S11, 0xf57c0faf_u); /*  5 */
    d = FF(d, a, b, c, x[ 5], S12, 0x4787c62a_u); /*  6 */
    c = FF(c, d, a, b, x[ 6], S13, 0xa8304613_u); /*  7 */
    b = FF(b, c, d, a, x[ 7], S14, 0xfd469501_u); /*  8 */
    a = FF(a, b, c, d, x[ 8], S11, 0x698098d8_u); /*  9 */
    d = FF(d, a, b, c, x[ 9], S12, 0x8b44f7af_u); /* 10 */
    c = FF(c, d, a, b, x[10], S13, 0xffff5bb1_u); /* 11 */
    b = FF(b, c, d, a, x[11], S14, 0x895cd7be_u); /* 12 */
    a = FF(a, b, c, d, x[12], S11, 0x6b901122_u); /* 13 */
    d = FF(d, a, b, c, x[13], S12, 0xfd987193_u); /* 14 */
    c = FF(c, d, a, b, x[14], S13, 0xa679438e_u); /* 15 */
    b = FF(b, c, d, a, x[15], S14, 0x49b40821_u); /* 16 */

    /* Round 2 */
    a = GG(a, b, c, d, x[ 1], S21, 0xf61e2562_u); /* 17 */
    d = GG(d, a, b, c, x[ 6], S22, 0xc040b340_u); /* 18 */
    c = GG(c, d, a, b, x[11], S23, 0x265e5a51_u); /* 19 */
    b = GG(b, c, d, a, x[ 0], S24, 0xe9b6c7aa_u); /* 20 */
    a = GG(a, b, c, d, x[ 5], S21, 0xd62f105d_u); /* 21 */
    d = GG(d, a, b, c, x[10], S22, 0x02441453_u); /* 22 */
    c = GG(c, d, a, b, x[15], S23, 0xd8a1e681_u); /* 23 */
    b = GG(b, c, d, a, x[ 4], S24, 0xe7d3fbc8_u); /* 24 */
    a = GG(a, b, c, d, x[ 9], S21, 0x21e1cde6_u); /* 25 */
    d = GG(d, a, b, c, x[14], S22, 0xc33707d6_u); /* 26 */
    c = GG(c, d, a, b, x[ 3], S23, 0xf4d50d87_u); /* 27 */
    b = GG(b, c, d, a, x[ 8], S24, 0x455a14ed_u); /* 28 */
    a = GG(a, b, c, d, x[13], S21, 0xa9e3e905_u); /* 29 */
    d = GG(d, a, b, c, x[ 2], S22, 0xfcefa3f8_u); /* 30 */
    c = GG(c, d, a, b, x[ 7], S23, 0x676f02d9_u); /* 31 */
    b = GG(b, c, d, a, x[12], S24, 0x8d2a4c8a_u); /* 32 */

    /* Round 3 */

    a = HH(a, b, c, d, x[ 5], S31, 0xfffa3942_u); /* 33 */
    d = HH(d, a, b, c, x[ 8], S32, 0x8771f681_u); /* 34 */
    c = HH(c, d, a, b, x[11], S33, 0x6d9d6122_u); /* 35 */
    b = HH(b, c, d, a, x[14], S34, 0xfde5380c_u); /* 36 */
    a = HH(a, b, c, d, x[ 1], S31, 0xa4beea44_u); /* 37 */
    d = HH(d, a, b, c, x[ 4], S32, 0x4bdecfa9_u); /* 38 */
    c = HH(c, d, a, b, x[ 7], S33, 0xf6bb4b60_u); /* 39 */
    b = HH(b, c, d, a, x[10], S34, 0xbebfbc70_u); /* 40 */
    a = HH(a, b, c, d, x[13], S31, 0x289b7ec6_u); /* 41 */
    d = HH(d, a, b, c, x[ 0], S32, 0xeaa127fa_u); /* 42 */
    c = HH(c, d, a, b, x[ 3], S33, 0xd4ef3085_u); /* 43 */
    b = HH(b, c, d, a, x[ 6], S34, 0x04881d05_u); /* 44 */
    a = HH(a, b, c, d, x[ 9], S31, 0xd9d4d039_u); /* 45 */
    d = HH(d, a, b, c, x[12], S32, 0xe6db99e5_u); /* 46 */
    c = HH(c, d, a, b, x[15], S33, 0x1fa27cf8_u); /* 47 */
    b = HH(b, c, d, a, x[ 2], S34, 0xc4ac5665_u); /* 48 */

    /* Round 4 */

    a = II(a, b, c, d, x[ 0], S41, 0xf4292244_u); /* 49 */
    d = II(d, a, b, c, x[ 7], S42, 0x432aff97_u); /* 50 */
    c = II(c, d, a, b, x[14], S43, 0xab9423a7_u); /* 51 */
    b = II(b, c, d, a, x[ 5], S44, 0xfc93a039_u); /* 52 */
    a = II(a, b, c, d, x[12], S41, 0x655b59c3_u); /* 53 */
    d = II(d, a, b, c, x[ 3], S42, 0x8f0ccc92_u); /* 54 */
    c = II(c, d, a, b, x[10], S43, 0xffeff47d_u); /* 55 */
    b = II(b, c, d, a, x[ 1], S44, 0x85845dd1_u); /* 56 */
    a = II(a, b, c, d, x[ 8], S41, 0x6fa87e4f_u); /* 57 */
    d = II(d, a, b, c, x[15], S42, 0xfe2ce6e0_u); /* 58 */
    c = II(c, d, a, b, x[ 6], S43, 0xa3014314_u); /* 59 */
    b = II(b, c, d, a, x[13], S44, 0x4e0811a1_u); /* 60 */
    a = II(a, b, c, d, x[ 4], S41, 0xf7537e82_u); /* 61 */
    d = II(d, a, b, c, x[11], S42, 0xbd3af235_u); /* 62 */
    c = II(c, d, a, b, x[ 2], S43, 0x2ad7d2bb_u); /* 63 */
    b = II(b, c, d, a, x[ 9], S44, 0xeb86d391_u); /* 64 */

    state[0] = wrapAdd(state[0], a);
    state[1] = wrapAdd(state[1], b);
    state[2] = wrapAdd(state[2], c);
    state[3] = wrapAdd(state[3], d);
}

private var md5Padding = array(0x80uss, ..replicateValue(0uss, #63));

// finish and return computed MD5 sum
// context cannot be reused after computation
overload digestFinish(context: MD5Context) --> returned: MD5 {
    var total = context.total;

    var last = total % 64;
    var padn = if (last < 56) 56 - last else 120 - last;
    digestUpdate(context, coordinateRange(begin(md5Padding), begin(md5Padding) + padn));

    var msglen = Union[Array[UInt8, 8], UInt64]();
    msglen.1 = total * 8;
    digestUpdate(context, msglen.0);

    assert(context.total % 64 == 0);

    returned.digest = littleWordsToBytes(context.state);
}

md5(message) = digest(message, #MD5_NAME);

