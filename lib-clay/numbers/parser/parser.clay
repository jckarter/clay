
import numbers.parser.errno.*;
import printer.(error);
import printer.formatter.(repr);
import strings.*;
import libc;
import sequences.*;



/// @section  Generic parsers 


[T when Type?(T)]
private define ConvFuncForType(#T);

overload ConvFuncForType(#CLongLong) = libc.strtoll;
overload ConvFuncForType(#CULongLong) = libc.strtoull;

[T when SignedInteger?(T) and TypeSize(T) <= TypeSize(CLong)]
overload ConvFuncForType(#T) = libc.strtol;
[T when UnsignedInteger?(T) and TypeSize(T) <= TypeSize(CULong)]
overload ConvFuncForType(#T) = libc.strtoul;

overload ConvFuncForType(#CLongDouble) = libc.strtold;
overload ConvFuncForType(#CDouble) = libc.strtod;
overload ConvFuncForType(#CFloat) = (nptr, endptr) -> CFloat(libc.strtod(nptr, endptr));

[T when Imaginary?(T)]
overload ConvFuncForType(#T) = ConvFuncForType(ImagBaseType(T));



[T, S when Numeric?(T) and String?(S)]
define parse(#T, s:S): T;


[T when Integer?(T) or Float?(T) or Imaginary?(T)]
overload parse(T, s) =
    parse(T, String(s));


[S, IntType when CCompatibleString?(S) and Integer?(IntType)]
overload parse(#IntType, s:S) {
    alias convFunc = ConvFuncForType(IntType);
    var p = cstring(s);
    var end = null(CChar);
    var value = convFunc(p, @end, 0);
    var typeName = StaticName(IntType);
    if (end^ != 0)
        error("invalid " ++ typeName ++ ": ", repr(s));
    if ((errno() == ERANGE) or value < Least(IntType) or value > Greatest(IntType))
        error(typeName ++ " out of range: ", repr(s));
    return IntType(value);
}

[S, FloatType when CCompatibleString?(S) and Float?(FloatType)]
overload parse(#FloatType, s:S) {
    alias convFunc = ConvFuncForType(FloatType);
    var p = cstring(s);
    var end = null(CChar);
    var value = convFunc(p, @end);
    var typeName = StaticName(FloatType);
    if (end^ != 0)
        error("invalid " ++ typeName ++ ": ", repr(s));
    if (errno() == ERANGE)
        error(typeName ++ " out of range: ", repr(s));
    return FloatType(value);
}

[S, ImagType when CCompatibleString?(S) and Imaginary?(ImagType)]
overload parse(#ImagType, s:S) {
    alias convFunc = ConvFuncForType(ImagType);
    var p = cstring(s);
    var end = null(CChar);
    var value = convFunc(p, @end);
    var typeName = StaticName(ImagType);
    if (end^ != 0)
        error("invalid " ++ typeName ++ ": ", repr(s));
    if (errno() == ERANGE)
        error(typeName ++ " out of range: ", repr(s));
    return numericConvert(ImagType,value);
}

