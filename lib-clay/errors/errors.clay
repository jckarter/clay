import libc.(abort);
import errors.backtrace.(showBacktrace);
import io.files.raw.(stderrRawFile);
import printer.(printlnTo,printTo);


//
// error (exceptions disabled)
//

errorNoThrow(..e) {
    var err = stderrRawFile();
    printlnTo(err, "error: ", ..e);
    showBacktrace();
    abort();
}

[ when not ExceptionsEnabled?]
error(..e) {
    errorNoThrow(..e);
}



//
// error (exceptions enabled)
//

record Error (msg : String);

instance Exception (Error);

[ when ExceptionsEnabled?]
overload error(..e) {
    var buf = String();
    printTo(buf, ..e);
    throw Error(move(buf));
}



//
// assert
//

AllAssertionsEnabled?() = not Flag?("clay.DisableAssertions");
AssertionEnabled?(tag) = not Flag?("clay.DisableAssertions." ++ tag);

[..tags when allValues?(StringLiteral?, ..tags)]
record assert[..tags] ();

[..tags]
alias overload assert[..tags](..whatever) {}

alias overload assert(..whatever) {}

[when AllAssertionsEnabled?()]
alias overload assert(cond:Bool, ..message) {
    if (not cond)
        errorNoThrow(StaticName(__FILE__), "(", __LINE__, ",", __COLUMN__, "): assertion ", __ARG__ cond, " failed: ", ..message);
}

[..tags when AllAssertionsEnabled?() and allValues?(AssertionEnabled?, ..tags)]
alias overload assert[..tags](cond:Bool, ..message) {
    if (not cond)
        errorNoThrow(StaticName(__FILE__), "(", __LINE__, ",", __COLUMN__, "): assertion ", __ARG__ cond, " failed: ", ..message);
}

[when AllAssertionsEnabled?()]
alias overload assert(cond:Bool) {
    if (not cond)
        errorNoThrow(StaticName(__FILE__), "(", __LINE__, ",", __COLUMN__, "): assertion ", __ARG__ cond, " failed");
}

[..tags when AllAssertionsEnabled?() and allValues?(AssertionEnabled?, ..tags)]
alias overload assert[..tags](cond:Bool) {
    if (not cond)
        errorNoThrow(StaticName(__FILE__), "(", __LINE__, ",", __COLUMN__, "): assertion ", __ARG__ cond, " failed");
}


//
// sourceLocation(), argAndValue() helpers
//

alias sourceLocation() = StaticName(__FILE__), __LINE__, __COLUMN__;

alias argAndValue(x) = __ARG__ x, x;

