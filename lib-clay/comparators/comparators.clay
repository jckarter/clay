import data.sequences.lazy.*;

// A comparator is a type which encapsulates the notion of comparing things.
// It may be used whenever you might want to naturally be able to redefine
// operators such as equality or ordering for use in a specific operation

define Comparator?;

overload Comparator?(T) = false;

forceinline overload notEquals?(comparator, a, b) = not equals?(comparator, a, b);
forceinline overload ordered?(comparator, a, b) = (a <= b) or (b <= a);
forceinline overload lesserEquals?(comparator, a, b) = not lesser?(b, a);
forceinline overload greater?(comparator, a, b) = lesser?(b, a);
forceinline overload greaterEquals?(comparator, a, b) = not lesser?(a, b);

// The natural comparison

record Natural();
overload Comparator?(#Natural) = true;

natural() = Natural();

forceinline overload equals?(c : Natural, x, y) = equals?(x, y);
forceinline overload lesser?(c : Natural, x, y) = lesser?(x, y);
forceinline overload lesserEquals?(c : Natural, x, y) = lesserEquals?(x, y);
forceinline overload greater?(c : Natural, x, y) = greater?(x, y);
forceinline overload greaterEquals?(c : Natural, x, y) = greaterEquals?(x, y);

// Comparing on a specific function

record Comparing[F](comparing : F);
[F] overload Comparator?(#Comparing[F]) = true;

comparing(f) = Comparing(f);


[F] forceinline overload equals?(c : Comparing[F], x, y) = equals?(c.comparing(x), c.comparing(y));
[F] forceinline overload lesser?(c : Comparing[F], x, y) = lesser?(c.comparing(x), c.comparing(y));
[F] forceinline overload lesserEquals?(c : Comparing[F], x, y) = lesserEquals?(c.comparing(x), c.comparing(y));
[F] forceinline overload greater?(c : Comparing[F], x, y) = greater?(c.comparing(x), c.comparing(y));
[F] forceinline overload greaterEquals?(c : Comparing[F], x, y) = greaterEquals?(c.comparing(x), c.comparing(y));

// Comparing in reversed of the normal order

record Reversed[C](original : C);
[C when Comparator?(C)] overload Comparator?(#Reversed[C]) = true;

define reversed;
[C when Comparator?(C)] overload reversed(c : C) = Reversed(c);
[C when Comparator?(C)] overload reversed(c : Reversed[C]) = c.original;
overload reversed() = reversed(natural());

[F] forceinline overload equals?(c : Reversed[F], x, y) = equals?(c.original, x, y);
[F] forceinline overload notEquals?(c : Reversed[F], x, y) = notEquals?(c.original, x, y);

[F] forceinline overload lesser?(c : Reversed[F], x, y) = greater?(c.original, x, y);
[F] forceinline overload lesserEquals?(c : Reversed[F], x, y) = greaterEquals?(c.original, x, y);

[F] forceinline overload greater?(c : Reversed[F], x, y) = lesser?(c.original, x, y);
[F] forceinline overload greaterEquals?(c : Reversed[F], x, y) = lesserEquals?(c.original, x, y);
