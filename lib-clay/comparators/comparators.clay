import data.sequences.lazy.*;

// A comparator is a type which encapsulates the notion of comparing things.
// It may be used whenever you might want to naturally be able to redefine
// operators such as equality or ordering for use in a specific operation

define Comparator?;

Comparator?(T) = false;

forceinline notEquals?(comparator, a, b) = not equals?(comparator, a, b);
forceinline ordered?(comparator, a, b) = (a <= b) or (b <= a);
forceinline lesserEquals?(comparator, a, b) = not lesser?(b, a);
forceinline greater?(comparator, a, b) = lesser?(b, a);
forceinline greaterEquals?(comparator, a, b) = not lesser?(a, b);

// The natural comparison

record Natural();
Comparator?(#Natural) = true;

natural() = Natural();

forceinline equals?(c : Natural, x, y) = equals?(x, y);
forceinline lesser?(c : Natural, x, y) = lesser?(x, y);
forceinline lesserEquals?(c : Natural, x, y) = lesserEquals?(x, y);
forceinline greater?(c : Natural, x, y) = greater?(x, y);
forceinline greaterEquals?(c : Natural, x, y) = greaterEquals?(x, y);

// Comparing on a specific function

record Comparing[F](comparing : F);
[F] Comparator?(#Comparing[F]) = true;

comparing(f) = Comparing(f);


[F] forceinline equals?(c : Comparing[F], x, y) = equals?(c.comparing(x), c.comparing(y));
[F] forceinline lesser?(c : Comparing[F], x, y) = lesser?(c.comparing(x), c.comparing(y));
[F] forceinline lesserEquals?(c : Comparing[F], x, y) = lesserEquals?(c.comparing(x), c.comparing(y));
[F] forceinline greater?(c : Comparing[F], x, y) = greater?(c.comparing(x), c.comparing(y));
[F] forceinline greaterEquals?(c : Comparing[F], x, y) = greaterEquals?(c.comparing(x), c.comparing(y));

// Comparing in reversed of the normal order

record Reversed[C](original : C);
[C when Comparator?(C)] Comparator?(#Reversed[C]) = true;

define reversed;
[C when Comparator?(C)] reversed(c : C) = Reversed(c);
[C when Comparator?(C)] reversed(c : Reversed[C]) = c.original;
reversed() = reversed(natural());

[F] forceinline equals?(c : Reversed[F], x, y) = equals?(c.original, x, y);
[F] forceinline notEquals?(c : Reversed[F], x, y) = notEquals?(c.original, x, y);

[F] forceinline lesser?(c : Reversed[F], x, y) = greater?(c.original, x, y);
[F] forceinline lesserEquals?(c : Reversed[F], x, y) = greaterEquals?(c.original, x, y);

[F] forceinline greater?(c : Reversed[F], x, y) = lesser?(c.original, x, y);
[F] forceinline greaterEquals?(c : Reversed[F], x, y) = lesserEquals?(c.original, x, y);
