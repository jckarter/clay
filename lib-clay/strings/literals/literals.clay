
//
// StringLiteralRef
//

record StringLiteralRef (
    sizep : Pointer[SizeT],
);


//
// predicates
//

overload ContiguousSequence?(#StringLiteralRef) = true;
[s when StringLiteral?(s)]
overload ContiguousSequence?(#Static[s]) = true;



//
// constructors
//

[s when StringLiteral?(s)]
inline overload StringLiteralRef(#s) = StringLiteralRef(stringTableConstant(s));
inline overload StringLiteralRef() = StringLiteralRef(stringTableConstant(""));


//
// size, begin, end, index
//

inline overload size(a:StringLiteralRef) = a.sizep^;

inline overload begin(a:StringLiteralRef) = Pointer[Char](a.sizep + 1);
inline overload end(a:StringLiteralRef) = begin(a) + size(a);

[I when Integer?(I)]
inline overload index(a:StringLiteralRef, i:I) {
    assert["boundsChecks"](i >= 0 and i < size(a), "StringLiteralRef index out of bounds");
    return ref (begin(a) + i)^;
}

[s when StringLiteral?(s)]
inline overload size(#s) = stringLiteralByteSize(s);
[s when StringLiteral?(s)]
inline overload begin(#s) = Pointer[Char](stringTableConstant(s) + 1);
[s when StringLiteral?(s)]
inline overload end(#s) = begin(s) + size(s);

[s, I when StringLiteral?(s) and Integer?(I)]
inline overload index(#s, i:I) {
    assert["boundsChecks"](i >= 0 and i < size(s), "literal \"", s, "\" index out of bounds");
    return ref (begin(s) + i)^;
}

[s, n when StringLiteral?(s) and n >= 0 and n < size(s)]
inline overload staticIndex(#s, #n) {
    return Char(stringLiteralByteIndex(#s,#n));
}



//
// convert to c-compatible string
//

cstring(s:Vector[Char]) {
    reserve(s, size(s)+1);
    end(s)^ = Char(0);
    return Pointer[Int8](s.data);
}



//
// cstring
//

inline overload cstring(a:StringLiteralRef) = Pointer[Int8](begin(a));

[s when StringLiteral?(s)]
inline overload cstring(#s) = Pointer[Int8](begin(s));


//
// coordinates and iteration
//

inline overload iterator(a:StringLiteralRef) = coordinateRange(begin(a), end(a));
[s when StringLiteral?(s)]
inline overload iterator(#s) = coordinateRange(begin(s), end(s));

inline overload reverseIterator(a:StringLiteralRef) =
    reverseCoordinateRange(end(a), begin(a));
[s when StringLiteral?(s)]
inline overload reverseIterator(#s) =
    reverseCoordinateRange(end(a), begin(a));


//
// string literal concatenation
//

private StringLiteralStatic?(S) = false;
[s]
overload StringLiteralStatic?(#Static[s]) = StringLiteral?(s);

AllStringLiterals?(..SS) = allValues?(S -> StringLiteralStatic?(S), ..SS);

[..SS when AllStringLiterals?(..SS)]
inline overload cat(..ss:SS) = stringLiteralConcat(..ss);


//
// string literal manipulation
//

// stringLiteralStartsWith?

stringLiteralStartsWith?(identifier, startsWith) = false;
[identifier, startsWith when size(identifier) >= size(startsWith)]
overload stringLiteralStartsWith?(#identifier, #startsWith)
      = stringLiteralByteSlice(identifier, #0, #size(startsWith))
        == startsWith;

// stringLiteralEndsWith?

stringLiteralEndsWith?(identifier, endsWith) = false;
[identifier, endsWith when size(identifier) >= size(endsWith)]
overload stringLiteralEndsWith?(#identifier, #endsWith)
    = stringLiteralByteSlice(identifier,
          #(size(identifier) - size(endsWith)),
          #size(identifier)
      ) == endsWith;

// stringLiteralFind

[identifier, sub, start when
    StringLiteral?(identifier) and StringLiteral?(sub)
]
stringLiteralFind(#identifier, #sub, #start)
    = stringLiteralFind(identifier, sub, #(start + 1));

[identifier, sub, start when
    StringLiteral?(identifier) and StringLiteral?(sub)
    and size(identifier) < size(sub) + start
]
overload stringLiteralFind(#identifier, #sub, #start) = -1;

[identifier, sub, start when
    StringLiteral?(identifier) and StringLiteral?(sub)
    and size(identifier) >= size(sub) + start
    and stringLiteralByteSlice(identifier, #start, #(start + size(sub)))
        == sub
]
overload stringLiteralFind(#identifier, #sub, #start) = start;

overload stringLiteralFind(identifier, sub) = stringLiteralFind(identifier, sub, #0);

// strl

define strl;

overload strl(..xx) = cat(..mapValues(strl, ..xx));

[x]
overload strl(#x) = StaticName(#x);
[i when StringLiteral?(i)]
overload strl(#i) = i;

overload strl() = "";

// stringLiteralCapitalize, stringLiteralDecapitalize

stringLiteralCapitalize("") = "";
[s when StringLiteral?(s) and size(s) >= 1]
overload stringLiteralCapitalize(#s)
    = stringLiteralFromBytes(#(Int(asciiUpper(s.0)))) ++ stringLiteralByteSlice(s, #1, #size(s));

stringLiteralDecapitalize("") = "";
[s when StringLiteral?(s) and size(s) >= 1]
overload stringLiteralDecapitalize(#s)
    = stringLiteralFromBytes(#(Int(asciiLower(s.0)))) ++ stringLiteralByteSlice(s, #1, #size(s));

// stringLiteralChars, stringLiteralStaticChars, stringLiteralFromChars
// XXX utf8

[s when StringLiteral?(s)]
stringLiteralChars(#s) = ..mapValues(Char, ..stringLiteralBytes(s));

[s when StringLiteral?(s)]
stringLiteralStaticChars(#s) =
    ..mapValues(n -> #Char(stringLiteralByteIndex(s, n)), ..staticIntegers(#size(s)));

[c]
private staticCharToByte(#c) = #Int(c);

stringLiteralFromChars(..c) = stringLiteralFromBytes(..mapValues(staticCharToByte, ..c));
