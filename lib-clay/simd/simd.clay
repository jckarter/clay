import simd.primitives as prims;


/// @section  index 

[T,n,I when Integer?(I)]
forceinline override index(a:Vec[T,n], i:I) = ref prims.vecElementRef(a, Int(i));



/// @section  type inferring constructor 

[..A when (countValues(..A) > 0) and equalValues?(..A)]
forceinline override Vec(..args:A) {
    alias n = #countValues(..A);
    alias T = firstValue(..A);
    return Vec[T, n](..args);
}



/// @section  basic constructor 

[T,n,..A when (countValues(..A) == n) and equalValues?(T, ..A)]
forceinline override Vec[T,n](..args:A) {
    var a = uninitializedUnsafe(Vec[T,n]);
    var i = 0;
    ..for (x in args) {
        a[i] = x;
        i +: 1;
    }
    return a;
}



/// @section  single element replicating constructor 

[T,n]
forceinline override Vec[T,n](x:T) {
    var a = uninitializedUnsafe(Vec[T,n]);
    ..for (i in integers(#n))
        a[i] = x;
    return a;
}



/// @section  other constructors, moveUnsafe, resetUnsafe, assign, destroy 

[T,n]
forceinline override Vec[T,n]() = Vec[T,n](T());

[T,n]
override Vec[T,n](src:Vec[T,n]) --> returned:Vec[T,n] __llvm__{
    %1 = load ${Vec[T,n]} * %src
    store ${Vec[T,n]} %1, ${Vec[T,n]} * %returned
    ret i8* null
}

[T,n]
forceinline override moveUnsafe(src:Vec[T,n]) --> returned:Vec[T,n] {
    returned <-- src;
}

[T,n]
forceinline override assign(ref dest:Vec[T,n], src:Vec[T,n]) {
    dest <-- src;
}



/// @section  unpackVec 

[T,n]
forceinline unpackVec(a:Vec[T,n]) = ..unpackElements(a, #0);

private define unpackElements;

[T,n,i]
forceinline override unpackElements(a:Vec[T,n], #i) =
    a[i], ..unpackElements(a, #(i+1));

[T,n,i when i == n]
forceinline override unpackElements(a:Vec[T,n], #i) = ;



/// @section  numeric ops 

[T,n]
forceinline override add(a:Vec[T,n], b:Vec[T,n]) = prims.add(a, b);

[T,n,X when Numeric?(X)]
forceinline override add(a:Vec[T,n], x:X) = a + Vec[T,n](T(x));

[T,n,X when Numeric?(X)]
forceinline override add(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) + a;


[T,n]
forceinline override subtract(a:Vec[T,n], b:Vec[T,n]) = prims.subtract(a, b);

[T,n,X when Numeric?(X)]
forceinline override subtract(a:Vec[T,n], x:X) = a - Vec[T,n](T(x));

[T,n,X when Numeric?(X)]
forceinline override subtract(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) - a;


[T,n]
forceinline override multiply(a:Vec[T,n], b:Vec[T,n]) = prims.multiply(a, b);

[T,n,X when Numeric?(X)]
forceinline override multiply(a:Vec[T,n], x:X) = a * Vec[T,n](T(x));

[T,n,X when Numeric?(X)]
forceinline override multiply(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) * a;


[T,n]
forceinline override divide(a:Vec[T,n], b:Vec[T,n]) = prims.divide(a, b);

[T,n,X when Numeric?(X)]
forceinline override divide(a:Vec[T,n], x:X) = a / Vec[T,n](T(x));

[T,n,X when Numeric?(X)]
forceinline override divide(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) / a;


[T,n]
forceinline override remainder(a:Vec[T,n], b:Vec[T,n]) = prims.remainder(a, b);

[T,n,X when Numeric?(X)]
forceinline override remainder(a:Vec[T,n], x:X) = a % Vec[T,n](T(x));

[T,n,X when Numeric?(X)]
forceinline override remainder(x:X, a:Vec[T,n]) = Vec[T,n](T(x)) % a;



/// @section  bitwise ops 

[T,n]
forceinline override bitshl(a:Vec[T,n], b:Vec[T,n]) = prims.bitshl(a, b);

[T,n,X when Numeric?(X)]
forceinline override bitshl(a:Vec[T,n], x:X) = bitshl(a, Vec[T,n](T(x)));

[T,n,X when Numeric?(X)]
forceinline override bitshl(x:X, a:Vec[T,n]) = bitshl(Vec[T,n](T(x)),  a);


[T,n]
forceinline override bitshr(a:Vec[T,n], b:Vec[T,n]) = prims.bitshr(a, b);

[T,n,X when Numeric?(X)]
forceinline override bitshr(a:Vec[T,n], x:X) = bitshr(a, Vec[T,n](T(x)));

[T,n,X when Numeric?(X)]
forceinline override bitshr(x:X, a:Vec[T,n]) = bitshr(Vec[T,n](T(x)),  a);

private forceinline bitwiseOp(fn, a, b) {
    alias size = Int(TypeSize(Type(a)));
    return bitcast(
        Type(a),
        fn(bitcast(Vec[UInt8, size], a), bitcast(Vec[UInt8, size], b))
    );
}

[T,n, U,m when CompatibleVectorTypes?(Vec[T,n], Vec[U,m])]
forceinline override bitand(a:Vec[T,n], b:Vec[U,m]) = bitwiseOp(prims.bitand, a, b);

[T,n,X when Numeric?(X)]
forceinline override bitand(a:Vec[T,n], x:X) = bitand(a, Vec[T,n](T(x)));

[T,n,X when Numeric?(X)]
forceinline override bitand(x:X, a:Vec[T,n]) = bitand(Vec[T,n](T(x)),  a);


[T,n, U,m when CompatibleVectorTypes?(Vec[T,n], Vec[U,m])]
forceinline override bitor(a:Vec[T,n], b:Vec[U,m]) = bitwiseOp(prims.bitor, a, b);

[T,n,X when Numeric?(X)]
forceinline override bitor(a:Vec[T,n], x:X) = bitor(a, Vec[T,n](T(x)));

[T,n,X when Numeric?(X)]
forceinline override bitor(x:X, a:Vec[T,n]) = bitor(Vec[T,n](T(x)),  a);


[T,n, U,m when CompatibleVectorTypes?(Vec[T,n], Vec[U,m])]
forceinline override bitxor(a:Vec[T,n], b:Vec[U,m]) = bitwiseOp(prims.bitxor, a, b);

[T,n,X when Numeric?(X)]
forceinline override bitxor(a:Vec[T,n], x:X) = bitxor(a, Vec[T,n](T(x)));

[T,n,X when Numeric?(X)]
forceinline override bitxor(x:X, a:Vec[T,n]) = bitxor(Vec[T,n](T(x)),  a);



/// @section  unary ops 

[T,n]
forceinline override add(a:Vec[T,n]) = a;

[T,n]
forceinline override subtract(a:Vec[T,n]) = Vec[T,n](T(0)) - a;

[T,n]
forceinline override bitnot(a:Vec[T,n]) = bitxor(Vec[T,n](componentMask(T)), a);


/// @section  casting 

define CompatibleVectorTypes?(X, Y) : Bool;
override CompatibleVectorTypes?(X, Y) = false;
[T,n, U,m when TypeSize(Vec[T,n]) == TypeSize(Vec[U,m])]
override CompatibleVectorTypes?(#Vec[T,n], #Vec[U,m]) = true;

[T,n, U,m when T != U and CompatibleVectorTypes?(Vec[T,n], Vec[U,m])]
forceinline override Vec[T,n](v: Vec[U,m]) = bitcast(Vec[T,n], v);



/// @section  boolean masks 

define componentMask;
[T when Integer?(T)]
forceinline override componentMask(#T) = wrapCast(T, -1);
forceinline override componentMask(#Float32) = bitcast(Float32, -1_i);
forceinline override componentMask(#Float64) = bitcast(Float64, -1_l);
