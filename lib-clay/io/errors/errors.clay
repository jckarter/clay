public import os.errors.*;
import printer.protocol.(printTo,printReprTo);


record GenericIOError(code:Int, where:StringLiteralRef);

[I, S when Integer?(I) and StringLiteral?(S)]
overload GenericIOError(code:I, #S) --> returned:GenericIOError {
    returned.code = Int(code);
    returned.where = StringLiteralRef(S);
}

overload printTo(stream, x:GenericIOError) {
    printTo(stream, x.where, ": ");
    printTo(stream, "error(", x.code, "): ");
    printErrorMessageTo(stream, x.code);
}

overload printReprTo(stream, x:GenericIOError) {
    printTo(stream, "GenericIOError(", errorCodeName(x.code), ", ", repr(x.where), ")");
}

record UnsupportedOperationIOError(where:StringLiteralRef);

[S when StringLiteral?(S)]
overload UnsupportedOperationIOError(#S) =
    UnsupportedOperationIOError(StringLiteralRef(S));

variant IOError;

instance OSError (IOError);

instance IOError (GenericIOError, UnsupportedOperationIOError);

[T when VariantMember?(IOError, T)]
overload Exception(forward x:T) = Exception(IOError(x));
