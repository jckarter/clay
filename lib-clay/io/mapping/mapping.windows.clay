import win32;


//
// FileMapping
//

private record InnerFileMapping {
    fileHandle : win32.HANDLE;
    mappingObject : win32.HANDLE;
    address : Pointer[Byte];
    length : SizeT;
}

overload RegularRecord?(static InnerFileMapping) = false;

record FileMapping {
    inner : SharedPointer[InnerFileMapping];
}



//
// constructors, destroy
//

enum MappingMode {
    MAP_READ,
    MAP_READ_WRITE,
    MAP_COPY_ON_WRITE
}

private modeProtection(mode) {
    if (mode == MAP_READ)
        return win32.PAGE_READONLY;
    if (mode == MAP_READ_WRITE)
        return win32.PAGE_READWRITE;
    if (mode == MAP_COPY_ON_WRITE)
        return win32.PAGE_WRITECOPY;
    assert(false);
    return 0u;
}

private modeAccess(mode) {
    if (mode == MAP_READ)
        return win32.FILE_MAP_READ;
    if (mode == MAP_READ_WRITE)
        return win32.FILE_MAP_WRITE;
    if (mode == MAP_COPY_ON_WRITE)
        return win32.FILE_MAP_COPY;
    assert(false);
    return 0u;
}

[A,B | Integer?(A,B)]
overload FileMapping(f:File, mode:MappingMode, offset:A, length:B) {
    var mappingObject = win32.CreateFileMappingA(
        fileHandle(f),
        win32.LPSECURITY_ATTRIBUTES(0),
        win32.DWORD(modeProtection(mode)),
        win32.DWORD(0),
        win32.DWORD(0),
        win32.LPCSTR(0));
    if (mappingObject == win32.HANDLE(0))
        error(win32.errorMessage());
    var offset64 = UInt64(offset);
    var offsetHigh = shiftRight(offset64, 32);
    var offsetLow = bitwiseAnd(offset64, shiftLeft(1u64,32)-1);
    var address = win32.MapViewOfFile(
        mappingObject,
        win32.DWORD(modeAccess(mode)),
        win32.DWORD(offsetHigh),
        win32.DWORD(offsetLow),
        win32.DWORD(length));
    if (address == win32.LPVOID(0)) {
        win32.CloseHandle(mappingObject);
        error(win32.errorMessage());
    }
    var p = new InnerFileMapping(fileHandle(f), mappingObject,
                                 address, SizeT(length));
    return FileMapping(p);
}

overload FileMapping(f:File, mode:MappingMode) =
    FileMapping(f, mode, 0, fileSize(f));

overload destroy(x:InnerFileMapping) {
    var result = win32.UnmapViewOfFile(x.address);
    if (result == 0)
        errorNoThrow(win32.errorMessage());
    result = win32.CloseHandle(x.mappingObject);
    if (result == 0)
        errorNoThrow(win32.errorMessage());
}


// disable default constructor, copy constructor, assignment
overload InnerFileMapping();
overload InnerFileMapping(x:InnerFileMapping);
overload assign(dest:InnerFileMapping, src:InnerFileMapping);



//
// mappingAddress, mappingLength
//

mappingAddress(fm:FileMapping) = fm.inner^.address;
mappingLength(fm:FileMapping) = fm.inner^.length;



//
// flushMapping
//

flushMapping(fm:FileMapping) {
    var result = win32.FlushViewOfFile(fm.inner^.address, fm.inner^.length);
    if (result == 0)
        error(win32.errorMessage());
    result = win32.FlushFileBuffers(fm.inner^.fileHandle);
    if (result == 0)
        error(win32.errorMessage());
}
