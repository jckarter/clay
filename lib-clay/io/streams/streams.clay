
public import io.errors.*;
import buffervector.*;
import strings.*;
import vectors.*;


/// @section  readBytes, writeBytes, flush - byte stream operations 
///
/// readBytes and writeBytes are SPI functions, orinary clients don't
/// need to call these functions directly, bug should use write
/// overloads instead

define readBytes(stream, buf:Pointer[Byte], n:SizeT) : SizeT;

define writeBytes(stream, buf:Pointer[Byte], n:SizeT) : SizeT;

define flush(stream) :;



/// @section InputStream, OutputStream types


[S]
InputStream?(#S) = CallDefined?(readBytes, S, Pointer[Byte], SizeT);

[S]
OutputStream?(#S) = CallDefined?(writeBytes, S, Pointer[Byte], SizeT);



/// @section misc type functions


[T]
ByteLike?(#T) = ByteSizedInteger?(T) or (T == Char);

[T]
ByteVector?(#T) = Vector?(T) and ByteLike?(SequenceElementType(T));

[T]
ByteBuffer?(#T) = ContiguousSequence?(T) and ByteLike?(SequenceElementType(T));



/// @section  read, write wrappers to readBytes, writeBytes

[I when InputStream?(I)]
define read(stream:I, ..args);
[O when OutputStream?(O)]
define write(stream:O, ..args) :;

[B, N when ByteLike?(B) and Integer?(N)]
overload read(stream, buf:Pointer[B], n:N) = readBytes(stream, Pointer[Byte](buf), SizeT(n));

[B, N when ByteLike?(B) and Integer?(N)]
overload write(stream, buf:Pointer[B], n:N) {
    var pos = SizeT(0);
    while (pos < n) {
        pos +: writeBytes(stream, Pointer[Byte](buf) + pos, SizeT(n) - pos);
    }
}


/// @section  readByte, readChar 

readByte(stream, b:Byte) : Bool {
    var result = read(stream, @b, SizeT(1));
    return result == 1;
}

readChar(stream, c:Char) : Bool {
    var ptr = Pointer[Byte](@c);
    var result = read(stream, ptr, SizeT(1));
    return result == 1;
}


/// @section  read - read into byte vector 

[V when ByteVector?(V)]
overload read(stream, n:SizeT, buf:V) {
    var current = size(buf);
    resize(buf, current + n);
    var ptr = Pointer[Byte](begin(buf) + current);
    var numRead = read(stream, ptr, n);
    resize(buf, current + numRead);
    return numRead;
}

overload read(stream, n:SizeT) {
    var buf = String();
    read(stream, n, buf);
    return move(buf);
}


/// @section  readN - read exactly n bytes 

record PrematureEOF(expected:SizeT, received:SizeT);
instance IOError (PrematureEOF);

define readN;

[V when ByteVector?(V)]
overload readN(stream, n:SizeT, buf:V) {
    var initial = size(buf);
    var current = initial;
    var expected = initial + n;
    while (current < expected) {
        var numRead = read(stream, expected - current, buf);
        if (numRead == 0)
            throw PrematureEOF(expected-initial, current-initial);
        current +: numRead;
    }
}

overload readN(stream, buffer:Pointer[Byte], n:SizeT) {
    var bv = BufferVector(buffer, buffer + n);
    readN(stream, n, bv);
}

overload readN(stream, n:SizeT) {
    var buf = String();
    readN(stream, n, buf);
    return move(buf);
}


/// @section  readAll 

define readAll;

[V when ByteVector?(V)]
overload readAll(stream, chunkSize:SizeT, buf:V) {
    while (true) {
        var numRead = read(stream, chunkSize, buf);
        if (numRead == 0)
            break;
    }
}

[V when ByteVector?(V)]
overload readAll(stream, buf:V) {
    readAll(stream, SizeT(4096), buf);
}

overload readAll(stream) : String {
    var buf = String();
    readAll(stream, buf);
    return move(buf);
}


/// @section  readUpto, readLine 

define readUpto;

[T, V when ByteLike?(T) and ByteVector?(V)]
overload readUpto(stream, upto:T, buf:V) {
    alias ElementType = SequenceElementType(V);
    var b = Byte();
    var uptoByte = Byte(upto);
    while (readByte(stream, b)) {
        push(buf, ElementType(b));
        if (b == uptoByte)
            break;
    }
}

[T when ByteLike?(T)]
overload readUpto(stream, upto:T) : String {
    var v = String();
    readUpto(stream, upto, v);
    return move(v);
}


define readLine;

[V when ByteVector?(V)]
overload readLine(stream, buf:V) {
    readUpto(stream, '\n', buf);
}

overload readLine(stream) = readUpto(stream, '\n');


/// @section  write - write one byte 

[I when Integer?(I)]
overload write(stream, x:I) {
    var b = Byte(x);
    write(stream, @b, SizeT(1));
}

overload write(stream, x:Char) {
    write(stream, Byte(x));
}


/// @section  write - write byte buffer 

[V when ByteBuffer?(V)]
overload write(stream, x:V) {
    write(stream, Pointer[Byte](begin(x)), size(x));
}


/// @section  write, flush - write to ByteVector 

[V when ByteVector?(V)]
overload writeBytes(stream:V, buf:Pointer[Byte], n:SizeT) {
    var currentSize = size(stream);
    resize(stream, currentSize + n);
    var dest = Pointer[Byte](begin(stream)) + currentSize;
    copyNonoverlappingMemory(dest, buf, buf+n);
    return n;
}

[V when ByteVector?(V)]
overload flush(stream:V) {
}
