import printer.(printTo,printReprTo);


/// @section  UniquePointer 
record UniquePointer[T] (ptr: Pointer[T]);

[T]
RegularRecord?(#UniquePointer[T]) = false;



/// @section  newUnique 
[T]
alias newUnique(x:T) {
    var ptr = allocateRawMemory(T, SizeT(1));
    try {
        ptr^ <-- x;
    } catch (ex) {
        freeRawMemory(ptr);
        throw ex;
    }
    return UniquePointer(ptr);
}



/// @section  constructors, moveUnsafe, resetUnsafe, assign, destroy 
[T]
UniquePointer[T]() = UniquePointer(null(T));

// no copying
[T]
UniquePointer[T](src: UniquePointer[T]);

[T]
BitwiseMovedType?(#UniquePointer[T]) = true;

[T]
forceinline resetUnsafe(p: UniquePointer[T]) {
    p.ptr = null(T);
}

// no assign from ref
[T]
forceinline assign(ref dest: UniquePointer[T], ref src: UniquePointer[T]);

[T]
forceinline destroy(p: UniquePointer[T]) {
    if (not null?(p.ptr)) {
        destroy(p.ptr^);
        freeRawMemory(p.ptr);
    }
}



/// @section  dereference, null?, nullUnique 
[T]
forceinline dereference(p: UniquePointer[T]) = ref p.ptr^;

[T]
forceinline null?(p: UniquePointer[T]) = null?(p.ptr);

[T]
forceinline nullUnique(#T) = UniquePointer[T]();



/// @section  equality 
[T]
forceinline equals?(p: UniquePointer[T], q: UniquePointer[T])
    = p.ptr == q.ptr;


/// @section  UniqueValue -- deep-copies on copy or assignment 

record UniqueValue[T] (ptr: UniquePointer[T]);

[T]
alias UniqueValue(x:T) = UniqueValue[T](newUnique(x));

[T]
forceinline UniqueValue[T](x:UniqueValue[T]) =
    if (null?(x))
        nullUniqueValue(T)
    else
        UniqueValue(x^);

[T]
assign(ref to:UniqueValue[T], ref from:UniqueValue[T]) {
    if (null?(from)) {
        destroy(to.ptr);
        resetUnsafe(to.ptr);
    } else if (null?(to)) {
        to <-- UniqueValue(from^);
    } else {
        to^ = from^;
    }
}

[T]
forceinline dereference(p: UniqueValue[T]) = ref p.ptr^;

[T]
forceinline null?(p: UniqueValue[T]) = null?(p.ptr);

[T]
forceinline nullUniqueValue(#T) = UniqueValue[T]();




/// @section  printTo - UniqueValue 

[T]
printTo(stream, v:UniqueValue[T]) {
    if (null?(v))
        printTo(stream, UniqueValue[T], "()");
    else {
        printTo(stream, UniqueValue[T], "(");
        printTo(stream, v^);
        printTo(stream, ")");
    }
}


/// @section  printReprTo - UniqueValue 

[T]
printReprTo(stream, v:UniqueValue[T]) {
    if (null?(v))
        printTo(stream, UniqueValue[T], "()");
    else {
        printTo(stream, UniqueValue[T], "(");
        printReprTo(stream, v^);
        printTo(stream, ")");
    }
}