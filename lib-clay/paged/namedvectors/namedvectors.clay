
import paged.misc.*;
import data.vectors.generic.*;
import data.strings;
import io.mapping.*;
import io.filesystem.*;
import io.files.*;


/// @section  NamedVector[T] 

record NamedVector[T] (
    fileName : data.strings.String,
    file : File,
    mapping : FileMapping,
    header : Pointer[Header],
    data : Pointer[T],
);

record Header (
    size : SizeT,
    capacity : SizeT,
);



/// @section  predicates 

[T] RegularRecord?(#NamedVector[T]) = false;

[T] ContiguousSequence?(#NamedVector[T]) = true;



/// @section  constructor 

private headerSpace() = alignUp(TypeSize(Header), 16u);

[T]
private requiredFileSize(#T, capacity) =
    capacity*TypeSize(T) + headerSpace();

[S,T when String?(S)]
NamedVector[T](fileName:S) {
    if (pathExists?(fileName)) {
        var f = File(fileName, READ_WRITE);
        var mapping = FileMapping(f, MAP_READ_WRITE);
        var header = Pointer[Header](mappingAddress(mapping));
        var data = Pointer[T](mappingAddress(mapping) + headerSpace());
        return NamedVector[T](data.strings.String(fileName), f, mapping, header, data);
    }
    else {
        var f = File(fileName, CREATE);
        var capacity = 16;
        var size = 0;
        // FIXME: not exception safe
        resizeFile(f, requiredFileSize(T, capacity));
        var mapping = FileMapping(f, MAP_READ_WRITE);
        var header = Pointer[Header](mappingAddress(mapping));
        var data = Pointer[T](mappingAddress(mapping) + headerSpace());
        header^.size = size;
        header^.capacity = capacity;
        return NamedVector[T](data.strings.String(fileName), f, mapping, header, data);
    }
}



/// @section  destroy 

[T]
destroy(x:NamedVector[T]) {
    destroy(x.mapping);
    destroy(x.file);
    destroy(x.fileName);
}



/// @section  generic vector requirements 

[T]
Vector?(#NamedVector[T]) = true;

[T]
vectorData(a:NamedVector[T]) = @(a.data^);

[T]
vectorSize(a:NamedVector[T]) = a.header^.size;

[T]
vectorSetSize(a:NamedVector[T], n:SizeT) {
    a.header^.size = n;
}

[T]
vectorCapacity(a:NamedVector[T]) = a.header^.capacity;

[T]
vectorRequestCapacity(a:NamedVector[T], capacity:SizeT) {
    assert(capacity >= vectorSize(a));
    if (capacity < vectorCapacity(a))
        return;
    var n = max(capacity, SizeT(16));
    n = max(n, 2*vectorCapacity(a));
    reallocVector(a, n);
}

[T]
private reallocVector(a:NamedVector[T], capacity:SizeT) {
    destroy(a.mapping);
    resizeFile(a.file, requiredFileSize(T, capacity));
    a.mapping <-- FileMapping(a.file, MAP_READ_WRITE);
    a.header = Pointer[Header](mappingAddress(a.mapping));
    a.data = Pointer[T](mappingAddress(a.mapping) + headerSpace());
    a.header^.capacity = capacity;
}
