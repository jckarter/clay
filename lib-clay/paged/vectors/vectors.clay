
import paged.allocator.*;
import data.vectors.generic.*;
import data.sequences.lazy.*;

/// @section  Vector[T] 

record Vector[T] (
    size:SizeT,
    capacity:SizeT,
    data:PagedPointer[T],
);



/// @section  predicates 

[T] RegularRecord?(#Vector[T]) = false;

[T] ContiguousSequence?(#Vector[T]) = true; 



/// @section  constructors 

[T]
Vector[T]() = Vector[T](SizeT(0), SizeT(0), nullPaged(T));

[T,A when Sequence?(A) and (T == SequenceElementType(A))]
Vector[T](a:A) {
    var v = Vector[T]();
    for (x in a)
        push(v, x);
    return move(v);
}

[T,A when SizedSequence?(A) and (T == SequenceElementType(A))]
Vector[T](a:A) {
    var v = Vector[T]();
    reserve(v, size(a));
    for (x in a)
        push(v, x);
    return move(v);
}

[A when Sequence?(A)]
Vector(a:A) {
    alias T = SequenceElementType(A);
    return Vector[T](a);
}



/// @section  varargs constructor 

[T, ..A when (countValues(..A) > 0) and equalValues?(T, ..A)]
alias Vector[T](..args:A) {
    var v = Vector[T]();
    reserve(v, countValues(..A));
    push(v, ..args);
    return move(v);
}



/// @section  moveUnsafe, resetUnsafe, assign, destroy 

[T]
BitwiseMovedType?(#Vector[T]) : Bool = true;

[T]
resetUnsafe(a:Vector[T]) {
    a <-- Vector[T]();
}


[T]
assign(ref dest:Vector[T], ref src:Vector[T]) {
    resize(dest, size(src));
    for (x, y in zipped(dest, src))
        assign(x, y);
}

[T]
destroy(a:Vector[T]) {
    for (x in a)
        destroy(x);
    freePaged(a.data);
}



/// @section  generic vector requirements 

[T]
Vector?(#Vector[T]) : Bool = true;

[T]
vectorData(a:Vector[T]) = @(a.data^);

[T]
vectorSize(a:Vector[T]) = a.size;

[T]
vectorSetSize(a:Vector[T], n:SizeT) {
    a.size = n;
}

[T]
vectorCapacity(a:Vector[T]) = a.capacity;

[T]
vectorRequestCapacity(a:Vector[T], capacity:SizeT) {
    assert(capacity >= a.size);
    if (capacity < a.capacity)
        return;
    var n = max(capacity, SizeT(16));
    n = max(n, 2*a.capacity);
    reallocVector(a, n);
}

[T]
private reallocVector(a:Vector[T], capacity:SizeT) {
    var data = allocatePaged(T, capacity);
    var ptr = data;
    for (x in a) {
        ptr^ <-- moveUnsafe(x);
        inc(ptr);
    }
    freePaged(a.data);
    a.data = data;
    a.capacity = capacity;
}
