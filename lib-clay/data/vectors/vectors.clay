
import data.vectors.generic.*;
public import data.vectors.generic.(Vector?);
import data.elements.*;

/// @module vector
/// vector implementation
/// see also: \a vectors.generic.generic


/// @record Vector[T]
/// A Vector of Elements

record Vector[T] (
    size:SizeT,
    capacity:SizeT,
    data:Pointer[T],
);


/// @section predicates

[T] overload RegularRecord?(#Vector[T]) = false;

[T]
overload ContiguousSequence?(#Vector[T]) = true;

[T]
overload SequenceContainer?(#Vector[T]) = true;


/// @section constructors


/// @overload Vector[T]()
/// construct an empty Vector.
[T]
overload Vector[T]() = Vector[T](SizeT(0), SizeT(0), null(T));

/// @overload  Vector[T](forward a:A)
/// construct Vector from a sequence element
[T,A when Sequence?(A) and (T == SequenceElementType(A))]
overload Vector[T](forward a:A) {
    var v = Vector[T]();
    pushAll(v, a);
    return move(v);
}

/// @overload  Vector[T](forward a:A)
/// construct Vector from a sequence.
[A when Sequence?(A)]
overload Vector(forward a:A) {
    alias T = SequenceElementType(A);
    return Vector[T](a);
}

/// @section varargs constructor

[A]
alias overload Vector[A](elements: NoElements) = Vector[A]();

/// Note this constructor cannot build vector from empty list of arguments
/// because it cannot infer vector element type. If vector can be empty,
/// and vector type is known, constructor from elements can be used:
/// Vector[T](elements(..args))
[..A when equalValues?(..A)]
alias vector(..elements:A) {
    var v = Vector[nthValue(#0, ..A)]();
    push(v, ..elements);
    return move(v);
}

/// @section moveUnsafe, resetUnsafe, assign, destroy

[T] overload BitwiseMovedType?(#Vector[T]) = true;

[T]
forceinline overload resetUnsafe(a:Vector[T]) {
    a <-- Vector[T]();
}

[T]
overload assign(ref dest:Vector[T], ref src:Vector[T]) {
    resizeUnsafe(dest, size(src));
    assignNonoverlappingMemory(begin(dest), begin(src), end(src));
}

[T]
overload destroy(a:Vector[T]) {
    destroyMemory(begin(a), end(a));
    freeRawMemory(a.data);
}

[T]
moveVectorBuffer(a:Vector[T]) {
    var data = a.data;
    resetUnsafe(a);
    return data;
}

/// @section generic vector requirements

[T]
overload Vector?(#Vector[T]) = true;

[T]
forceinline overload vectorData(a:Vector[T]) = a.data;

[T]
forceinline overload vectorSize(a:Vector[T]) = a.size;

[T]
forceinline overload vectorSetSize(a:Vector[T], n:SizeT) {
    a.size = n;
}

[T]
forceinline overload vectorCapacity(a:Vector[T]) = a.capacity;

[T]
overload vectorRequestCapacity(a:Vector[T], capacity:SizeT) {
    assert(capacity >= a.size);
    if (capacity < a.capacity)
        return;
    var n = max(capacity, SizeT(16));
    n = max(n, 2*a.capacity);
    reallocVector(a, n);
}

private define reallocVector;

[T]
default reallocVector(a:Vector[T], capacity:SizeT) {
    var data = allocateRawMemory(T, capacity);
    moveNonoverlappingMemoryUnsafe(data, a.data, a.data + a.size);
    freeRawMemory(a.data);
    a.data = data;
    a.capacity = capacity;
}

[T when BitwiseMovedType?(T) and CallDefined?(reallocateRawMemory, Pointer[T], SizeT)]
overload reallocVector(a:Vector[T], capacity:SizeT) {
    a.data = reallocateRawMemory(a.data, capacity);
    a.capacity = capacity;
}
