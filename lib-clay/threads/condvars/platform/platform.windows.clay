import threads.condvars.protocol.(ConditionVariableMutex, wait, notifyOne, notifyAll);
import threads.locks.platform.(Mutex);
import win32.(
    CONDITION_VARIABLE,
    INFINITE,
    InitializeConditionVariable,
    SleepConditionVariableCS,
    WakeConditionVariable,
    WakeAllConditionVariable,
);

record ConditionVariable (_cv:CONDITION_VARIABLE);
RegularRecord?(#ConditionVariable) = false;
DestroyDoesNothingType?(#ConditionVariable) = true;

ConditionVariable() --> returned:ConditionVariable
{
    InitializeConditionVariable(@returned._cv);
}

ConditionVariableMutex(#ConditionVariable) = Mutex;

wait(cv:ConditionVariable, m:Mutex)
{
    var ok = SleepConditionVariableCS(@cv._cv, @m._cs, INFINITE);
    assert(ok != 0);
}

notifyOne(cv:ConditionVariable)
{
    WakeConditionVariable(@cv._cv);
}

notifyAll(cv:ConditionVariable)
{
    WakeAllConditionVariable(@cv._cv);
}
