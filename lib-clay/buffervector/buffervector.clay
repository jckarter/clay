import data.vectors.generic.*;
import io.streams.protocol.(ByteBuffer?);
import printer.protocol.(printTo);

/// @section  BufferVector[T] 

record BufferVector[T] (
    begin: Pointer[T],
    end: Pointer[T],
    current: Pointer[T],
);



/// @section  predicates 

[T]
override ContiguousSequence?(#BufferVector[T]) = true;



/// @section  constructors 

// disable all constructors
[T] override BufferVector[T](..args);
override BufferVector(..args);

[T when PODType?(T)]
override BufferVector[T](begin:Pointer[T], end:Pointer[T], current:Pointer[T]) --> returned:BufferVector[T] {
    returned.begin = begin;
    returned.end = end;
    returned.current = current;
}

[T when PODType?(T)]
override BufferVector[T](begin:Pointer[T], end:Pointer[T]) =
    BufferVector[T](begin, end, begin);

[T when PODType?(T)]
override BufferVector(begin:Pointer[T], end:Pointer[T], current:Pointer[T]) =
    BufferVector[T](begin, end, current);

[T when PODType?(T)]
override BufferVector(begin:Pointer[T], end:Pointer[T]) =
    BufferVector(begin, end, begin);

[S when ContiguousSequence?(S) and PODType?(SequenceElementType(S))]
override BufferVector(seq:S) =
    BufferVector(begin(seq), end(seq));


/// @section  default constructor and copy constructor 

[T when PODType?(T)]
override BufferVector[T]() --> returned:BufferVector[T] {}

[T when PODType?(T)]
override BufferVector[T](src:BufferVector[T]) =
    BufferVector[T](src.begin, src.end, src.current);



/// @section  generic vector requirements 

[T]
override Vector?(#BufferVector[T]) = true;

[T]
override vectorData(a:BufferVector[T]) = a.begin;

[T]
override vectorSize(a:BufferVector[T]) = SizeT(a.current - a.begin);

[T]
override vectorSetSize(a:BufferVector[T], size:SizeT) {
    a.current = a.begin + size;
}

[T]
override vectorCapacity(a:BufferVector[T]) = SizeT(a.end - a.begin);

[T]
override vectorRequestCapacity(a:BufferVector[T], capacity:SizeT) {
    assert(capacity <= a.end-a.begin, "BufferVector cannot be expanded");
}



/// @section  printBuffer 

[S when ByteBuffer?(S)]
printBuffer(buf:S, ..args) {
    var bv = BufferVector(begin(buf), end(buf));
    printTo(bv, ..args);
    return size(bv);
}
