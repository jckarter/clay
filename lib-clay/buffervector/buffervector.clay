import data.vectors.generic.*;
import io.streams.protocol.(ByteBuffer?);
import printer.protocol.(printTo);

/// @section  BufferVector[T] 

record BufferVector[T] (
    begin: Pointer[T],
    end: Pointer[T],
    current: Pointer[T],
);



/// @section  predicates 

[T]
ContiguousSequence?(#BufferVector[T]) = true;



/// @section  constructors 

// disable all constructors
[T] BufferVector[T](..args);
[T] BufferVector(..args);

[T when PODType?(T)]
BufferVector[T](begin:Pointer[T], end:Pointer[T], current:Pointer[T]) --> returned:BufferVector[T] {
    returned.begin = begin;
    returned.end = end;
    returned.current = current;
}

[T when PODType?(T)]
BufferVector[T](begin:Pointer[T], end:Pointer[T]) =
    BufferVector[T](begin, end, begin);

[T when PODType?(T)]
BufferVector(begin:Pointer[T], end:Pointer[T], current:Pointer[T]) =
    BufferVector[T](begin, end, current);

[T when PODType?(T)]
BufferVector(begin:Pointer[T], end:Pointer[T]) =
    BufferVector(begin, end, begin);

[S when ContiguousSequence?(S) and PODType?(SequenceElementType(S))]
BufferVector(seq:S) =
    BufferVector(begin(seq), end(seq));


/// @section  default constructor and copy constructor 

[T when PODType?(T)]
BufferVector[T]() --> returned:BufferVector[T] {}

[T when PODType?(T)]
BufferVector[T](src:BufferVector[T]) =
    BufferVector[T](src.begin, src.end, src.current);



/// @section  generic vector requirements 

[T]
Vector?(#BufferVector[T]) = true;

[T]
vectorData(a:BufferVector[T]) = a.begin;

[T]
vectorSize(a:BufferVector[T]) = SizeT(a.current - a.begin);

[T]
vectorSetSize(a:BufferVector[T], size:SizeT) {
    a.current = a.begin + size;
}

[T]
vectorCapacity(a:BufferVector[T]) = SizeT(a.end - a.begin);

[T]
vectorRequestCapacity(a:BufferVector[T], capacity:SizeT) {
    assert(capacity <= a.end-a.begin, "BufferVector cannot be expanded");
}



/// @section  printBuffer 

[S when ByteBuffer?(S)]
printBuffer(buf:S, ..args) {
    var bv = BufferVector(begin(buf), end(buf));
    printTo(bv, ..args);
    return size(bv);
}
