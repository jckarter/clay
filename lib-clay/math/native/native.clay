public import
    math.native.protocol.*,

    math.native.copysign.*,
    math.native.ldexp.*,
    math.native.frexp.*,
    math.native.scalbn.*,
    math.native.trunc.*,
    math.native.ceil.*,
    math.native.floor.*,
    math.native.rint.*,
    math.native.round.*,
    math.native.llround.*,
    math.native.modf.*,
    math.native.fmod.*,
    math.native.nextafter.*,
    math.native.abs.*,
    math.native.abscomplex.*,
    math.native.sign.*,
    math.native.hypot.*,
    math.native.sqrt.*,
    math.native.cbrt.*,
    math.native.pow.*,
    math.native.exp.*,
    math.native.exp2.*,
    math.native.expm1.*,
    math.native.sinh.*,
    math.native.cosh.*,
    math.native.tanh.*,
    math.native.ilogb.*,
    math.native.log.*,
    math.native.log2.*,
    math.native.log10.*,
    math.native.log1p.*,
    math.native.erf.*,
    math.native.gamma.*,
    math.native.beta.*,
    math.native.bessel.*,
    math.native.sinpi.*,
    math.native.tan.*,
    math.native.cos.*,
    math.native.sin.*,
    math.native.atanh.*,
    math.native.acosh.*,
    math.native.asinh.*,
    math.native.asin.*,
    math.native.acos.*,
    math.native.atan.*,
    math.native.atan2.*,
    math.native.cot.*,
    math.native.arg.*,
    math.native.proj.*,

    complex.*;


import math.native.constants.(M_PI);
import numbers.floats.(floatFromBits,floatExponent,mantissaSize);

define recip;

[T when Integer?(T)]
forceinline override recip(x:T) = T(0);
[T when Float?(T)]
forceinline override recip(x:T) = T(1) / x;

[T when Float?(T)]
forceinline significand(x:T) = scalbn(x,wrapSubtract(ilogb(x)));

alias drem = remainder;

[T]
forceinline clamp(x:T, lo:T, hi:T) {
    if(x > hi)
        return hi; 
    if(x < lo)
        return lo;
    return x;
}

forceinline sec(z) = recip(cos(z));
forceinline csc(z) = recip(sin(z));
forceinline asec(y) = acos(recip(y));
forceinline acsc(y) = asin(recip(y));
forceinline acot(y) = atan(recip(y));
forceinline sech(z) = recip(cosh(z));
forceinline csch(z) = recip(sinh(z));
forceinline coth(z) = recip(tanh(z));
forceinline asech(y) = acosh(recip(y));
forceinline acsch(y) = asinh(recip(y));
forceinline acoth(y) = atanh(recip(y));

[T when CallDefined?(sin,T)]
forceinline sinc(x:T) {
    if(x==0) 
        return T(1);
    else {
        var pix = T(M_PI)*x; 
        return sin(pix)/pix;
    }
}

[T when CallDefined?(sin,T)]
forceinline cosc(x:T) { 
    if(x==0) 
        return T(0);
    else {
        var pix=T(M_PI)*x;
        return cos(pix)/x-sin(pix)/(pix*x);
    }
}

forceinline square(x) = x*x;
forceinline cube(x) = x*x*x;

forceinline fma(a, b, c) = (a * b) + c;

[T when Float?(T)] 
forceinline rsqrt(x:T) = recip(sqrt(x));

[T when Float?(T)] 
forceinline sigmoid (x:T) = T(1) / (T(1) + exp (-x));

define normcoeff;
forceinline override normcoeff(#LongDouble) = floatFromBits(UInt128(0x4035ul));
forceinline override normcoeff(#Double) = floatFromBits(0x435ul);
forceinline override normcoeff(#Float) = floatFromBits(0x4Cu);

[T when Float?(T)]
forceinline normalize(x:T) 
    = if(normalized?(x)) x else floatFromBits(floatExponent(x*normcoeff(T)) - 
        (mantissaSize(T) + 2));


[T when Float?(T)] 
forceinline epsilon(#T) = nextafter(T(1),T(2))-T(1);
