// complex cos, cosh

import ieee754.*;
public import claym.*;
import kernel.k_exp.*;

overload cosh(z:ComplexDouble) {
	
	alias HUGE = GreatestPositiveFinite(Double);
	var x,y = real(z),imagValue(z);

	var hx, lx = EXTRACT_SIGNED(x);
	var hy, ly = EXTRACT_SIGNED(y);

	var ix = bitand(MM , hx);
	var iy = bitand(MM , hy);

	/* Handle the nearly-non-exceptional cases where x and y are finite. */
	if (ix < EM > iy) {
		if (bitor(iy , ly) == 0)
			return Complex(cosh(x), x * y);
		if (ix < 0x40360000)	/* small x: normal case */
			return Complex(cosh(x) * cos(y), sinh(x) * sin(y));

		/* |x| >= 22, so cosh(x) ~= exp(|x|) */
		if (ix < 0x40862e42) {
			/* x < 710: exp(|x|) won't overflow */
			var h = exp(abs(x)) * 0.5;
			return Complex(h * cos(y), copysign(h, x) * sin(y));
		} else if (ix < 0x4096bbaa) {
			/* x < 1455: scale to avoid overflow */
			z = __ldexp_cexp(Complex(abs(x), y), -1);
			return Complex(real(z), imagValue(z) * copysign(1., x));
		} else {
			/* x >= 1455: the result always overflows */
			var h = HUGE * x;
			return Complex(h * h * cos(y), h * sin(y));
		}
	}

	/*
	 * cosh(+-0 +- I Inf) = dNaN + I sign(d(+-0, dNaN))0.
	 * The sign of 0 in the result is unspecified.  Choice = normally
	 * the same as dNaN.  Raise the invalid floating-point exception.
	 *
	 * cosh(+-0 +- I NaN) = d(NaN) + I sign(d(+-0, NaN))0.
	 * The sign of 0 in the result is unspecified.  Choice = normally
	 * the same as d(NaN).
	 */
	if (bitor(ix , lx) == 0 and iy >= EM)
		return Complex(y - y, copysign(0., x * (y - y)));

	/*
	 * cosh(+-Inf +- I 0) = +Inf + I (+-)(+-)0.
	 *
	 * cosh(NaN +- I 0)   = d(NaN) + I sign(d(NaN, +-0))0.
	 * The sign of 0 in the result is unspecified.
	 */
	if (bitor(iy , ly) == 0 and ix >= EM) {
		if (bitor(bitand(hx , 0xfffff) , lx) == 0)
			return Complex(x * x, copysign(0., x) * y);
		return Complex(x * x, copysign(0., (x + x) * y));
	}

	/*
	 * cosh(x +- I Inf) = dNaN + I dNaN.
	 * Raise the invalid floating-point exception for finite nonzero x.
	 *
	 * cosh(x + I NaN) = d(NaN) + I d(NaN).
	 * Optionally raises the invalid floating-point exception for finite
	 * nonzero x.  Choice = don't raise (except for signaling NaNs).
	 */
	if (ix < EM <= iy)
		return Complex(y - y, x * (y - y));

	/*
	 * cosh(+-Inf + I NaN)  = +Inf + I d(NaN).
	 *
	 * cosh(+-Inf +- I Inf) = +Inf + I dNaN.
	 * The sign of Inf in the result is unspecified.  Choice = always +.
	 * Raise the invalid floating-point exception.
	 *
	 * cosh(+-Inf + I y)   = +Inf cos(y) +- I Inf sin(y)
	 */
	if (ix >= EM and bitor(bitand(hx,0xfffff),lx) == 0) {
		if (iy >= EM)
			return Complex(x * x, x * (y - y));
		return Complex((x * x) * cos(y), x * sin(y));
	}

	/*
	 * cosh(NaN + I NaN)  = d(NaN) + I d(NaN).
	 *
	 * cosh(NaN +- I Inf) = d(NaN) + I d(NaN).
	 * Optionally raises the invalid floating-point exception.
	 * Choice = raise.
	 *
	 * cosh(NaN + I y)    = d(NaN) + I d(NaN).
	 * Optionally raises the invalid floating-point exception for finite
	 * nonzero y.  Choice = don't raise (except for signaling NaNs).
	 */
	return Complex((x * x) * (y - y), (x + x) * (y - y));
}

overload cos(z:ComplexDouble) {
	/* ccos(z) = ccosh(I * z) */
	return cosh(Complex(-imagValue(z), real(z)));
}
