


//
// Tuple?
//

Tuple?(x) = false;
[...T] overload Tuple?(static Tuple[...T]) = true;



//
// TupleElements
//

[...T] TupleElements(static Tuple[...T]) = ...T;



//
// staticIndex
//

[...T, i]
inline overload staticIndex(a:Tuple[...T], static i) =
    ref tupleRef(a, static SizeT(i));



//
// unpackTuple, unpackTupleRef
//

inline unpackTuple(x) = ...unpackTupleRef(x);

[...T]
inline unpackTupleRef(x:Tuple[...T]) = ref ...tupleElements(x);



//
// unpack, unpackRef
//

inline unpackRef(x) = ref x;
[...T]
inline overload unpackRef(x:Tuple[...T]) = ref ...unpackTupleRef(x);

inline unpack(x) = ...unpackRef(x);



//
// Tuple type inferring constructor
//

[...T]
callbyname overload Tuple(...args:T) = Tuple[...T](...args);



//
// basic constructor
//

[...T]
callbyname overload Tuple[...T](...args:T) returned:Tuple[...T] {
    ...unpackTupleRef(returned) <-- ...args;
}



//
// default constructor
//

[...T]
inline overload Tuple[...T]() returned:Tuple[...T] {
    initializeMulti(...tupleElements(returned));
}

[...A]
private inline initializeMulti(...args:A) {
    ...args <-- ...initializeTypes(...A);
}

private procedure initializeTypes;
inline overload initializeTypes(A, ...B) = A(), ...initializeTypes(...B);
inline overload initializeTypes() = ;



//
// copy constructor
//

[...T]
inline overload Tuple[...T](src:Tuple[...T]) returned:Tuple[...T] {
    ...tupleElements(returned) <-- ...tupleElements(src);
}



//
// moveUnsafe, resetUnsafe
//

[...T]
inline overload moveUnsafe(src:Tuple[...T]) returned:Tuple[...T] {
    alias n = TupleElementCount(Tuple[...T]);
    static for (i in ...staticIntegers(static n))
        staticIndex(returned, i) <-- moveUnsafe(staticIndex(src, i));
}

[...T]
inline overload resetUnsafe(a:Tuple[...T]) {
    static for (x in ...tupleElements(a))
        resetUnsafe(x);
}



//
// destroy
//

[...T]
inline overload destroy(a:Tuple[...T]) {
    static for (x in ...tupleElements(a))
        destroy(x);
}



//
// assign
//

[...T]
inline overload assign(dest:Tuple[...T], lvalue src:Tuple[...T]) {
    alias n = TupleElementCount(Tuple[...T]);
    static for (i in ...staticIntegers(static n))
        assign(staticIndex(dest, i), staticIndex(src, i));
}



//
// equals?
//

[...T]
inline overload equals?(a:Tuple[...T], b:Tuple[...T]) {
    alias n = TupleElementCount(Tuple[...T]);
    static for (i in ...staticIntegers(static n)) {
        if (not equals?(staticIndex(a, i), staticIndex(b, i)))
            return false;
    }
    return true;
}



//
// lesser?
//

[...T]
inline overload lesser?(a:Tuple[...T], b:Tuple[...T]) {
    alias n = TupleElementCount(Tuple[...T]);
    static for (i in ...staticIntegers(static n)) {
        ref av = staticIndex(a, i);
        ref bv = staticIndex(b, i);
        if (lesser?(av, bv))
            return true;
        if (lesser?(bv, av))
            return false;
    }
    return false;
}



//
// tupleLiteral
//

procedure tupleLiteral;

// by default create a tuple
callbyname overload tupleLiteral(...args) = Tuple(...args);

// in the case of single argument, return the argument itself.
overload tupleLiteral(forward x) = forward x;
