
//
// Opaque
//

alias Opaque = Int;


//
// display
//

procedure display;

overload display(x) {
    println(x);
}


//
// DisplayableVTable
//

record DisplayableVTable {
    size : SizeT;
    destructor : CodePointer[Opaque,()];
    copyConstructor : CodePointer[Opaque,Opaque];
    display : CodePointer[Opaque,()];
}

[T]
makeDisplayableVTable(static T) {
    var destructor = makeCodePointer(destroy, T);
    var copyConstructor = makeCodePointer(T, T);
    var display = makeCodePointer(display, T);
    return DisplayableVTable(TypeSize(T),
                             CodePointer[Opaque,()](destructor),
                             CodePointer[Opaque,Opaque](copyConstructor),
                             CodePointer[Opaque,()](display));
}


//
// Displayable
//

record Displayable {
    obj : Pointer[Opaque];
    vtable : Pointer[DisplayableVTable];
}

overload RegularRecord?(static Displayable) = false;


// constructor
[T]
overload Displayable(x:T) {
    var obj = allocateRawMemory(T, 1);
    obj^ <-- x;
    var vtable = allocateRawMemory(DisplayableVTable, 1);
    vtable^ <-- makeDisplayableVTable(T);
    return Displayable(Pointer[Opaque](obj), vtable);
}

// default constructor
overload Displayable() = Displayable(null(Opaque), null(DisplayableVTable));

// copy constructor
overload Displayable(x:Displayable) {
    if (null?(x.obj)) return Displayable();
    var p = allocateRawMemory(Int8, x.vtable^.size);
    var obj = Pointer[Opaque](p);
    obj^ <-- x.vtable^.copyConstructor(x.obj^);
    var vtable = allocateRawMemory(DisplayableVTable, 1);
    vtable^ <-- x.vtable^;
    return Displayable(obj, vtable);
}

// moveUnsafe
overload moveUnsafe(src:Displayable) returned:Displayable {
    returned <-- Displayable(src.obj, src.vtable);
}

// resetUnsafe
overload resetUnsafe(x:Displayable) {
    x <-- Displayable();
}

// destructor
overload destroy(x:Displayable) {
    if (not null?(x.obj)) {
        x.vtable^.destructor(x.obj^);
        freeRawMemory(x.obj);
        freeRawMemory(x.vtable);
    }
}


overload display(x:Displayable) {
    x.vtable^.display(x.obj^);
}

main() {
    var v = Vector[Displayable]();
    push(v, Displayable(1));
    push(v, Displayable(3.2));
    push(v, Displayable("Hi!"));
    for (x in v)
        display(x);
}
