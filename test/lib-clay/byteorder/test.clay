import byteorder.*;
import printer.(str);
import test.*;

define testNetworkToHost;

[IntType]
override testNetworkToHost(#IntType, function, expectReversed?) {
    alias S = #TypeSize(IntType);
    var digits = array(..mapValues(i -> UInt8(250 - i), ..integers(S)));
    var digitsReversed = array(..reverseValues(..arrayElements(digits)));
    var expected = if (expectReversed?) digitsReversed else digits;
    expectEqual(
        expected,
        bitcast(Array[UInt8, Int(TypeSize(IntType))], function(Union[IntType, Array[UInt8, Int(TypeSize(IntType))]](digits).0)));
}

[IntType when not IntegerAvailable?(IntType)]
override testNetworkToHost(#IntType, function, expectReversed?) {
}

main() = testMain(
    TestSuite("byteorder", array(
        TestCase("networkToHost, hostToNetwork", -> {
            ..for (IntType in UInt8, Int8, UInt16, Int16, UInt32, Int32, UInt64, Int64, UInt128, Int128) {
                testNetworkToHost(IntType, networkToHost, LittleEndian?);
                testNetworkToHost(IntType, hostToNetwork, LittleEndian?);
                testNetworkToHost(IntType, bigToHost, LittleEndian?);
                testNetworkToHost(IntType, hostToBig, LittleEndian?);
                testNetworkToHost(IntType, littleToHost, BigEndian?);
                testNetworkToHost(IntType, hostToLittle, BigEndian?);
            }
        }),
    )));
