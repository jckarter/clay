import test.*;
import commandline.options.table.*;
import commandline.options.parser.*;
import data.strings.*;
import data.vectors.*;

fbbSpec() = vector(
  OptionSpec('f', "foo"),
  OptionSpec('b', "bar"),
  OptionSpec('z', "baz"),
  OptionSpec('i', "bif")
);

valuableSpec() = vector(
  OptionSpec('f', "foo", HAS_VALUE),
  OptionSpec('b', "bar", HAS_VALUE)
);

[T] numberOf(it, #T){
  var i = 0;
  for(x in it) if(variantIs?(x, T)) inc(i);
  return i;
}

numberOfOptions(it) = numberOf(it, OptionValue);
numberOfArguments(it) = numberOf(it, Argument);
validOptions?(it) = numberOf(it, Error) == 0;

PR(..args) = ParseResult([..mapValues(ParseToken, ..args)]);

main() = testMain(
  TestSuite("Raw option parsing", array(
    TestCase("a single option amongst many", -> {
      var opts = array("foo", "bar", "baz");
      for(opt in opts){
        var pr = parseOptions(fbbSpec(), array("--"++opt));
        expectEqual(pr, vector(ParseToken(OptionValue(opt))));
      }
    }),
    TestCase("repeated options", -> {
      var pr1 = parseOptions(fbbSpec(), array("-f", "-b", "-i"));
      var pr2 = parseOptions(fbbSpec(), array("-fbi"));

      expectTrue(validOptions?(pr1));
      expectEqual(pr1, pr2);
    }),
    TestCase("with values omitted", -> {
      var pr = parseOptions(valuableSpec(), array("--foo", "--bar"));
      expectFalse(validOptions?(pr));
    }),
    TestCase("with values as separate tokens", -> {
      var pr = parseOptions(valuableSpec(), array("--foo", "bar"));
      expectEqual(ParseResult([ParseToken(OptionValue("foo", "bar"))]), pr);
    }),
    TestCase("with values included in a short options", -> {
      var pr = parseOptions(valuableSpec(), array("-fbar"));
      expectEqual(PR(OptionValue("f", "bar")), pr);
    }),
    TestCase("with values included in a long option", -> {
      var pr = parseOptions(valuableSpec(), array("--foo=bar"));
      expectEqual(PR(OptionValue("foo", "bar")), pr);
    }),
    TestCase("with a separator", -> {
      var pr = parseOptions(valuableSpec(), array("--foo", "bar", "--", "--bar"));

      expectTrue(validOptions?(pr));
      expectEqual(1, numberOfOptions(pr));
      expectEqual(1, numberOfArguments(pr));
    }),
    TestCase("with negative numeric arguments", -> {
      var pr = parseOptions(OptionSpecs(), array("-1"));

      expectEqual(PR(Argument(String("-1"))), pr);
    })
  )),
  TestSuite("The option table API", array(
    TestCase("testing for presence", -> {
      var ot = OptionTable(fbbSpec(), array("-fb", "-baz"));
      expectTrue(optionGiven?(ot, "foo"));
      expectTrue(optionGiven?(ot, "foo"));
    }),
    TestCase("getting values", -> {
      var ot = OptionTable(valuableSpec(), array("-fkittens", "--bar=badgers", "--foo", "bunnies"));

      expectEqual(getOptions(ot, "foo"), vector(String("kittens"), String("bunnies")));
      expectEqual(getOptions(ot, "bar"), vector(String("badgers")));
    })
  ))
);

