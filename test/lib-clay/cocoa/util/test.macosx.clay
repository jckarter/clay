import
    cocoa.*,
    cocoa.test.*,
    hash.(hash),
    printer.(str),
    test.*,
    test.memory.*,
    data.strings.(String);

record Foo (x: Int64, y: Double);
[I when Integer?(I) and I != Int64]
override Foo(x: I, y: Double) = Foo(Int64(x), y);

override selector("lifecycleTest") = Id;
override selector("lifecycleTestWithCanary:") = Id, Canary;
override selector("lifecycleTestWithCanary:subcanary:") = Id, Canary, Canary;
override selector("initWithCanary:") = Id, Canary;
override selector("initWithCanary:subcanary:") = Id, Canary, Canary;
override selector("initWithSubcanary:") = Id, Canary;

record LifecycleTest = newClass(NSObject,
    InstanceVars(
        ["canary", Canary],
    ),
    ClassMethods(
        AutoreleasedClassMethod("lifecycleTestWithCanary:", "initWithCanary:"),
        AutoreleasedClassMethod("lifecycleTest"),
    ),
    InstanceMethods(
        InitInstanceMethod("initWithCanary:"),
        InitInstanceMethod(),
        CopyInstanceMethod(CopyRoot),
        DeallocInstanceMethod(),
    ),
);

record LifecycleSubTest = newClass(LifecycleTest,
    InstanceVars(
        ["subcanary", Canary],
    ),
    ClassMethods(
        AutoreleasedClassMethod(
            "lifecycleTestWithCanary:subcanary:", "initWithCanary:subcanary:"
        ),
    ),
    InstanceMethods(
        InitInstanceMethod("initWithCanary:subcanary:", "initWithCanary:"),
        InitInstanceMethod("initWithSubcanary:"),
        InitInstanceMethod(),
        CopyInstanceMethod(CopyInheriting),
        DeallocInstanceMethod(),
    ),
);

override selector("foo") = Int;
override selector("setFoo:") = Void, Int;
override selector("foo64") = Int;
override selector("setFoo64:") = Void, Int;
override selector("bar") = Float;
override selector("setBar:") = Void, Float;
override selector("isBas") = Bool;
override selector("setBas:") = Void, Bool;

record PropertyTest = newClass(NSObject,
    InstanceVars(
        ["fooVar", Int64],
        ["bar", Retained[Id]],
        ["bas", Bool],
    ),
    ClassMethods(),
    InstanceMethods(
        // reader/writer names related but different from variable name, default selector type
        ..PropertyInstanceMethods("fooVar", "foo"),
        // reader/writer names related but different from variable name, specified selector type
        ..PropertyInstanceMethods("fooVar", "foo64", Int64),
        // default reader/writer names, specified selector type
        ..PropertyInstanceMethods("bar", Id),
        // reader/writer names both different, default selector type
        ..PropertyInstanceMethods("bas", "isBas", "setBas:"),
    )
);

override selector("initWithFoo:bar:") = Id, Id, Id;

record RegularTest = regularClass(["foo", String], ["bar", Char]);
record RegularCanary = regularClass(["foo", Canary], ["bar", Canary]);

main() = withAutoreleasePool(() => testMain(TestSuite("cocoa.util", array(
    TestCase("constructor overloads", -> {
        expectEqual(true, NSNumber(true).boolValue());
        expectEqual(5, NSNumber(5).intValue());
        expectEqual(5.5, NSNumber(5.5).doubleValue());
        expectEqual(0x1_0000_0000_ul,
            NSNumber(0x1_0000_0000_ul).unsignedLongLongValue()
        );

        expectEqual("bees",
            CStringRef(NSString("bees").UTF8String())
        );
        expectEqual("bees",
            CStringRef(NSString("bees").UTF8String())
        );
        expectEqual("bees",
            CStringRef(NSString(String("bees")).UTF8String())
        );

        expectEqual("ants",
            String(NSString("ants"))
        );

        expectEqual(5, fromNSValue(Int, makeNSValue(5)));
        expectEqual(5.5, fromNSValue(Double, makeNSValue(5.5)));
        expectEqual(null(Int),
            fromNSValue(Pointer[Int], makeNSValue(null(Int)))
        );
        expectEqual(Foo(14, 92.0),
            fromNSValue(Foo, makeNSValue(Foo(14, 92.0)))
        );
    }),
    TestCase("boxing of clay primitives", -> {
        expectEqual(5, Id(5).intValue());
        expectEqual(5.5, Id(5.5).doubleValue());
        expectEqual(0x1_0000_0000_ul,
            Id(0x1_0000_0000_ul).unsignedLongLongValue()
        );
        expectEqual(true,
            Id(true).boolValue()
        );

        expectEqual("worms",
            String(Id("worms"))
        );

        expectEqual("worms",
            String(Id("worms"))
        );

        expectEqual("worms",
            String(Id(String("worms")))
        );

        expectEqual(Foo(7, 11.0),
            fromNSValue(Foo, Id(Foo(7, 11.0)))
        );
    }),
    TestCase("equality, hashing and comparison", -> {
        var x = NSString("foo");
        var y = NSString("foo");
        var z = x;
        var w = NSString("bar");

        expectType(NSString, x);

        expectTrue(x == y);
        expectTrue(x != w);

        expectFalse(x < y);
        expectFalse(x < w);
        expectTrue (x <= y);
        expectFalse(x <= w);
        expectFalse(x > y);
        expectTrue (x > w);
        expectTrue (x >= y);
        expectTrue (x >= w);

        expectTrue(x < "fop");
        expectTrue(x == "foo");
        expectTrue(x != "bar");
        expectTrue(x > "fon");

        expectTrue(isNot?(x, y));
        expectTrue(is?(x, z));

        expectEqual(x.hash(), hash(x));
    }),
    TestCase("array", -> {
        var array = NSMutableArray.alloc().init();

        array.addObject(9);
        push(array, "birds");
        array.insertObject_atIndex(Foo(12, 24.0), NSUInteger(1));
        insert(array, 1, NSNull.null());

        expectEqual(9, array[0].intValue());
        expectEqual(NSNull.null(), array[1]);
        expectEqual(Foo(12, 24.0), fromNSValue(Foo, array[2]));
        expectEqual("birds", String(array[3]));

        expectEqual(4, size(array));

        expectSequence(array,
            Id(NSNumber(9)),
            Id(NSNull.null()),
            Id(makeNSValue(Foo(12, 24.0))),
            Id(NSString("birds")),
        );

        array.release();
    }),
    TestCase("dictionary", -> {
        var dict = NSMutableDictionary.alloc().init();

        dict.setObject_forKey(9, "zim");
        dict.setObject_forKey("birds", "zang");

        expectEqual(9, dict["zim"].intValue());
        expectEqual("birds", String(dict["zang"]));

        expectEqual(2, size(dict));

        dict.release();
    }),
    TestCase("array, dictionary constructors", -> {
        var array = makeNSArray(1, "foo", 2.0, NSNull.null());
        var dict = makeNSDictionary(
            "zim", 9,
            "zang", "bar",
            "zung", false,
        );

        expectEqual(4, size(array));
        expectEqual(3, size(dict));

        expectSequence(array,
            Id(NSNumber(1)),
            Id(NSString("foo")),
            Id(NSNumber(2.0)),
            Id(NSNull.null())
        );
        expectEqual(9,     dict["zim" ]);
        expectEqual("bar", dict["zang"]);
        expectEqual(false, dict["zung"]);
    }),
    TestCase("lifecycle methods", -> {
        expectCanariesDestroyed(() => {
            withAutoreleasePool(() => {
                var autoreleased1 = LifecycleTest.lifecycleTest();
                var autoreleased2 = LifecycleTest.lifecycleTestWithCanary(Canary());
                var autoreleased3 = LifecycleSubTest.lifecycleTest();
                var autoreleased4
                    = LifecycleSubTest.lifecycleTestWithCanary_subcanary(Canary(), Canary());

                var inited1 = LifecycleTest.alloc().init();
                var inited2 = LifecycleTest.alloc().initWithCanary(Canary());
                var inited3 = LifecycleSubTest.alloc().init();
                var inited4
                    = LifecycleSubTest.alloc().initWithCanary_subcanary(Canary(), Canary());
                var inited5
                    = LifecycleSubTest.alloc().initWithSubcanary(Canary());

                var copied1 = autoreleased1.copy();
                var copied2 = inited1.copy();
                var copied3 = autoreleased3.copy();
                var copied4 = inited3.copy();

                ..for (o in inited1, inited2, inited3, inited4, inited5,
                                 copied1, copied2, copied3, copied4)
                    o.release();
            });
        });
    }),
    TestCase("printing objects", -> {
        expectEqual("whisky", str(NSString("whisky")));
        expectEqual("2020", str(NSNumber(2020)));
        expectEqual("(nil)", str(nil(NSString)));
    }),
    TestCase("property accessor methods", -> {
        var x = Retained(PropertyTest.alloc().init());
        {
            var s = Retained(NSString.alloc().initWithFormat(NSSTR("catch-%d"), 22));
            x.setFoo(27);
            x.setBar(s);
            x.setBas(true);
            // s gets released here, so setBar had better have retained it
        }
        expectEqual(27, x.foo());
        expectEqual(27_l, x.foo64());
        expectEqual(NSUInteger(1), x.bar().retainCount());
        expectEqual("catch-22", x.bar());
        expectEqual(true, x.isBas());
    }),
    TestCase("regularClass method signatures", -> {
        expectEqual(String,
            SelectorReturnType(RegularTest, "foo")
        );
        expectEqualValues([],
            ..SelectorArgumentTypes(RegularTest, "foo")
        );
        expectEqual(Void,
            SelectorReturnType(RegularTest, "setFoo:")
        );
        expectEqualValues(Tuple(String),
            ..SelectorArgumentTypes(RegularTest, "setFoo:")
        );

        expectEqual(RegularTest,
            SelectorReturnType(RegularTest, "initWithFoo:bar:")
        );
        expectEqualValues(
            [String, Char],
            ..SelectorArgumentTypes(RegularTest, "initWithFoo:bar:")
        );

        expectEqual(RegularCanary,
            SelectorReturnType(RegularCanary, "initWithFoo:bar:")
        );
        expectEqualValues(
            [Canary, Canary],
            ..SelectorArgumentTypes(RegularCanary, "initWithFoo:bar:")
        );
    }),
    TestCase("regularClass usage", -> {
        var defaultObject = Retained(RegularTest.alloc().init());

        expectEqual("", defaultObject.foo());

        var regularObject = Retained(RegularTest.alloc().initWithFoo_bar(String("wye"), 'x'));

        expectEqual("wye", regularObject.foo());
        expectEqual('x', regularObject.bar());

        regularObject.setFoo(String("zed"));
        regularObject.setBar('w');

        expectEqual("zed", regularObject.foo());
        expectEqual('w', regularObject.bar());

        var copy = Retained(regularObject.copy());

        expectEqual("zed", copy.foo());
        expectEqual('w', copy.bar());
    }),
    TestCase("regularClass memory management", -> {
        expectCanariesDestroyed(() => {
            var foo = Retained(RegularCanary.alloc().init());
            var bar = Retained(foo.copy());
            foo.setFoo(Canary());
            var bas = Retained(RegularCanary.alloc().initWithFoo_bar(Canary(), Canary()));
        });
    }),
    TestCase("indexSetIndices", -> {
        expectSequence(
            indexSetIndices(NSIndexSet.indexSet()),
        );
        expectSequence(
            indexSetIndices(NSIndexSet.indexSetWithIndex(NSUInteger(5))),
            NSUInteger(5)
        );
        expectSequence(
            indexSetIndices(NSIndexSet.indexSetWithIndexesInRange(
                NSMakeRange(NSUInteger(10), NSUInteger(5))
            )),
            NSUInteger(10), NSUInteger(11), NSUInteger(12), NSUInteger(13), NSUInteger(14)
        );
    }),
))));
