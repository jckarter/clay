import
    test.*,
    test.memory.*,
    remote.marshaling.*,
    uniquepointers.(UniquePointer,newUnique),
    printer.(printTo),
    referencetypes.(referenceType),
    sharedpointers.*,
    data.strings.*,
    data.vectors.*;

expectMarshalingInvariants(value) {
    var marshaled = marshal(value);
    var expectedValue = UnmarshaledType(Type(value))(value);

    expectEqual(
        expectedValue, unmarshal(marshaled, Type(value))
    );
    expectTrue(
        size(marshaled) == marshaledSize(MarshalContext(), value)
    );

    if (not empty?(marshaled)) {
        var truncated = marshaled;
        pop(truncated);
        expectExceptionType(
            UnmarshalUnexpectedEOF,
            () => { unmarshal(truncated, Type(value)); }
        );
        expectExceptionType(
            UnmarshalUnexpectedEOF,
            () => { unmarshal(Vector[Byte](), Type(value)); }
        );
    }
}

expectNullMarshaling(value) {
    var marshaled = marshal(value);

    expectEqual(
        SizeT(0), size(marshaled)
    );
    expectEqual(
        value, unmarshal(marshaled, Type(value))
    );
}

record PODRecord (x: Int, y: Char, z: Double);
record EmptyRecord ();
record ComplexRecord (
    x: Int,
    y: Vector[Int],
    z: Char,
);

enum Enum ( FOO, BAR, BAS );

record UniquePointerRecord (x: UniquePointer[Int]);
override UniquePointerRecord(x: Int) = UniquePointerRecord(newUnique(x));
override UniquePointerRecord(o: UniquePointerRecord)
    = if (null?(o.x)) UniquePointerRecord()
      else UniquePointerRecord(newUnique(o.x^));
override equals?(a: UniquePointerRecord, b: UniquePointerRecord)
    = if (null?(a.x)) null?(b.x) else (if (null?(b.x)) false else a.x^ == b.x^);
override printTo(stream, o: UniquePointerRecord) {
    printTo(stream, "UniquePointer(");
    if (not null?(o.x))
        printTo(stream, o.x^);
    printTo(stream, ")");
}

record GraphNodeRecord (
    left: SharedPointer[GraphNodeRecord],
    right: SharedPointer[GraphNodeRecord],
);

record ReferenceRecord = referenceType(x: Int);
override equals?(a:ReferenceRecord, b:ReferenceRecord) = a.x == b.x;

variant TestVariant (Int, Vector[Int], Char, Double);

main() = testMain(
    TestSuite("marshaling", array(
        TestCase("POD types", -> {
            expectMarshalingInvariants(1);
            expectMarshalingInvariants('a');
            expectMarshalingInvariants(0.0);
            expectMarshalingInvariants([1, 'a', 0.0]);
            expectMarshalingInvariants(PODRecord(1, 'a', 0.0)
            );
            expectMarshalingInvariants(array(1, 2, 3));
        }),
        TestCase("reference types", -> {
            expectMarshalingInvariants(ReferenceRecord(3));
        }),
        TestCase("sequences", -> {
            expectMarshalingInvariants(
                Vector[Int](1, 2, 3)
            );
            expectMarshalingInvariants(
                String("crenshaw")
            );
            expectMarshalingInvariants(
                Vector[String](String("crenshaw"), String("mathers"))
            );
            expectMarshalingInvariants(
                String(),
            );
            expectMarshalingInvariants(
                Vector[Int](),
            );
            expectMarshalingInvariants(
                Vector[String](),
            );
        }),
        TestCase("special sequences", -> {
            expectMarshalingInvariants("mathers");
        }),
        TestCase("composites", -> {
            expectMarshalingInvariants(
                [1, Vector[Int](1, 2, 3), 'a']
            );
            expectMarshalingInvariants(
                [1, Vector[Int](1, 2, 3), 'a']
            );
            expectMarshalingInvariants(
                ComplexRecord(1, Vector[Int](1, 2, 3), 'a')
            );
            expectMarshalingInvariants(
                [ReferenceRecord(3), ReferenceRecord(5)]
            );
            expectMarshalingInvariants(
                Vector[ReferenceRecord](ReferenceRecord(3), ReferenceRecord(5)),
            );
            expectMarshalingInvariants(
                [ReferenceRecord(3), Vector[ReferenceRecord](ReferenceRecord(3), ReferenceRecord(5))],
            );
            expectMarshalingInvariants(
                Range[Int](1, 3),
            );
            expectMarshalingInvariants(
                Range[SizeT](SizeT(1), SizeT(3)),
            );
            expectNullMarshaling([]);
            expectNullMarshaling(EmptyRecord());
        }),
        TestCase("variants", -> {
            expectMarshalingInvariants(
                TestVariant(1)
            );
            expectMarshalingInvariants(
                TestVariant('a')
            );
            expectMarshalingInvariants(
                TestVariant(0.0)
            );
            expectMarshalingInvariants(
                TestVariant(Vector[Int](1, 2, 3))
            );
        }),
        TestCase("statics", -> {
            expectNullMarshaling(#0);
            expectNullMarshaling(add);
            expectNullMarshaling(Int);
        }),
        TestCase("lambdas", -> {
            var int = 1;
            var char = 'a';
            var vectorInt = Vector[Int](1, 2, 3);
            var vectorString = Vector[String](String("crenshaw"), String("mathers"));

            expectMarshalingInvariants(() => int);
            expectMarshalingInvariants(() => char);
            expectMarshalingInvariants(() => vectorInt);
            expectMarshalingInvariants(() => vectorString);
            expectMarshalingInvariants(() => (int, char));
            expectMarshalingInvariants(
                () => (int, vectorInt)
            );
            expectMarshalingInvariants(
                () => (int, vectorString, char)
            );
            expectNullMarshaling(() => {});
        }),
        TestCase("unique pointers", -> {
            expectMarshalingInvariants(
                UniquePointerRecord()
            );
            expectMarshalingInvariants(
                UniquePointerRecord(3)
            );
        }),
        TestCase("shared pointers", -> {
            var a = new(GraphNodeRecord());
            var b = new(GraphNodeRecord());
            var c = new(GraphNodeRecord());
            var d = new(GraphNodeRecord());

            a^.left = b;
            a^.right = d;
            b^.left = c;
            b^.right = c;
            c^.left = d;

            var aa = unmarshal(marshal(a), SharedPointer[GraphNodeRecord]);
            var bb = aa^.left;
            var cc = bb^.left;
            var dd = aa^.right;

            expectEqual(bb, aa^.left);
            expectEqual(dd, aa^.right);
            expectEqual(cc, bb^.left);
            expectEqual(cc, bb^.right);
            expectEqual(dd, cc^.left);
            expectEqual(nullShared(GraphNodeRecord), cc^.right);
            expectEqual(nullShared(GraphNodeRecord), dd^.left);
            expectEqual(nullShared(GraphNodeRecord), dd^.right);
        }),
        TestCase("enums", -> {
            expectMarshalingInvariants(FOO);
        }),
    )),
);
