import printer.(println);

var _nextId = 0;

nextId() {
    var id = _nextId;
    _nextId +: 1;
    return id;
}

record Test (
    id : Int
);

overload RegularRecord?(#Test) = false;

overload Test() {
    var id = nextId();
    println("construct Test ", id);
    return Test(id);
}

overload Test(src:Test) {
    println("copy Test ", src.id);
    return Test(src.id);
}

overload moveUnsafe(src:Test) {
    println("move Test ", src.id);
    return Test(src.id);
}

overload resetUnsafe(x:Test) {
    var id = nextId();
    println("reset Test ", id);
    x.id = id;
}

overload destroy(x:Test) {
    println("destroy Test ", x.id);
}

overload assign(dest:Test, src:Test) {
    dest.id = src.id;
}

main() {
    var a = Vector[Test](Test(), Test());
    reserve(a, 100);
    push(a, array(Test(), Test(), Test(), Test(), Test()));
    var x = Test();
    push(a, move(x));
}
