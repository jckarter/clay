
record Foo ();

var dummy = 10;

overload index(x:Foo, index) = ref dummy;
overload staticIndex(x:Foo, index) = ref dummy;
overload fieldRef(x:Foo, field) = ref dummy;

overload indexAssign(x:Foo, index, value) {
    println("indexAssign(x, ", index, ", ", value, ")");
}

overload indexUpdateAssign(op, x:Foo, index, ..args) {
    println("indexUpdateAssign(", ..weaveValues(",",op,"x",index,..args), ")");
}

[i]
overload staticIndexAssign(x:Foo, static i, value) {
    println("staticIndexAssign(x, ", i, ", ", value, ")");
}

[i]
overload staticIndexUpdateAssign(op, x:Foo, static i, ..args) {
    println("staticIndexUpdateAssign(", ..weaveValues(",",op,"x",i,..args), ")");
}

[FIELD]
overload fieldRefAssign(x:Foo, static FIELD, value) {
    println("fieldRefAssign(x, ", FIELD, ", ", value, ")");
}

[FIELD]
overload fieldRefUpdateAssign(op, x:Foo, static FIELD, ..args) {
    println("fieldRefUpdateAssign(", ..weaveValues(",",op,"x",FIELD, ..args), ")");
}

main() {
    var foo = Foo();
    foo[10] = "hello";
    foo.20 = "hi";
    foo.bar = "hola";
    foo[30] += 100;
    foo.40 *= 200;
    foo.baz \= 300;
}
