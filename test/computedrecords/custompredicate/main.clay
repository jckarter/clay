

record Point[T] = flexi(x:T, y:T);

main() {
    println(FlexiConstructor?(Point[Int]));
    var a = Point[Int](10, 20);
    var b = Point[Int](1.2, 3.4);
    var c = Point[Int]('a', 'z');
    for (x in array(a, b, c))
        println(x);
}


//
// FlexiConstructor? predicate
//

define FlexiConstructor?;
overload FlexiConstructor?(x) = false;



//
// flexi
//

flexi(..fields) = recordWithPredicate(FlexiConstructor?, ..fields);



//
// argument converting constructor
//

[T, ..A  when  FlexiConstructor?(T) and (countValues(..A) == RecordFieldCount(T))]
overload T(forward ..args:A) --> returned:T {
    alias TargetTypes = Tuple(..RecordFieldTypes(T));
    ..recordFields(returned) <-- ..convertToTypes(TargetTypes, #0, ..args);
}



//
// convertToTypes
//

define convertToTypes;

[i]
overload convertToTypes(types, #i, forward first, forward ..rest) {
    alias T = staticIndex(types, #i);
    return forward convertToType(T, first), ..convertToTypes(types, #i+1, ..rest);
}

[T, i  when  (i == TupleElementCount(T))]
overload convertToTypes(types:T, #i) = ;



//
// convertToType
//

define convertToType;

[Dest, Src]
overload convertToType(#Dest, forward src:Src) = forward Dest(src);

[T] 
overload convertToType(#T, forward src:T) = forward src;
