import printer.(println);
import data.vectors.*;

var _nextId = 0;

nextId() {
    var id = _nextId;
    _nextId +: 1;
    return id;
}

record Test (
    id : Int
);

overload RegularRecord?(#Test) = false;

overload Test() {
    var id = nextId();
    println("construct Test ", id);
    return Test(id);
}

overload Test(src:Test) {
    println("copy Test ", src.id);
    return Test(src.id);
}

overload moveUnsafe(src:Test) {
    println("move Test ", src.id);
    return Test(src.id);
}

overload resetUnsafe(x:Test) {
    var id = nextId();
    println("reset Test ", id);
    x.id = id;
}

overload destroy(x:Test) {
    println("destroy Test ", x.id);
}

overload assign(dest:Test, src:Test) {
    dest.id = src.id;
}

main() {
    println("first line");
    var a = vector(Test(), Test());

    println("before reserve");
    reserve(a, 100);

    println("before pushAll");
    pushAll(a, array(Test(), Test(), Test(), Test(), Test()));

    println("before x construction");
    var x = Test();

    println("before push x");
    push(a, move(x));

    println("last line");
}
