import printer.(print,println);
import data.vectors.*;

//
// Opaque
//

alias Opaque = Int;


//
// display
//

define display;

display(x) {
    print(x);
}

[T]
display(a:Vector[T]) {
    print("[");
    for (i in range(size(a))) {
        if (i != 0)
            print(", ");
        display(a[i]);
    }
    print("]");
}


//
// Displayable
//

record Displayable (
    obj : Pointer[Opaque],
    size : SizeT,
    alignment : SizeT,
    destructor : CodePointer[[Opaque],[]],
    copyConstructor : CodePointer[[Opaque],[Opaque]],
    display : CodePointer[[Opaque],[]],
);

RegularRecord?(#Displayable) = false;


// primary constructor
[T]
Displayable(x:T) {
    var obj = allocateRawMemoryAligned(T, 1, TypeAlignment(T));
    obj^ <-- x;
    var destructor = makeCodePointer(destroy, T);
    var copyConstructor = makeCodePointer(T, T);
    var displayPtr = makeCodePointer(display, T);

    return Displayable(Pointer[Opaque](obj),
                       TypeSize(T),
                       TypeAlignment(T),
                       CodePointer[[Opaque],[]](destructor),
                       CodePointer[[Opaque],[Opaque]](copyConstructor),
                       CodePointer[[Opaque],[]](displayPtr));
}

// default constructor
Displayable() {
    return Displayable(null(Opaque), SizeT(0), SizeT(0),
                       CodePointer[[Opaque],[]](0),
                       CodePointer[[Opaque],[Opaque]](0),
                       CodePointer[[Opaque],[]](0));
}

// copy constructor
Displayable(x:Displayable) {
    var p = allocateRawMemoryAligned(Int8, x.size, x.alignment);
    var obj = Pointer[Opaque](p);
    obj^ <-- x.copyConstructor(x.obj^);
    return Displayable(obj, x.size, x.alignment,
                       x.destructor, x.copyConstructor, x.display);
}

// moveUnsafe
moveUnsafe(src:Displayable) --> returned:Displayable {
    returned <-- Displayable(
        src.obj,
        src.size,
        src.alignment,
        src.destructor,
        src.copyConstructor,
        src.display
    );
}

// resetUnsafe
resetUnsafe(x:Displayable) {
    x <-- Displayable();
}

// destructor
destroy(x:Displayable) {
    if (not null?(x.obj)) {
        x.destructor(x.obj^);
        freeRawMemoryAligned(x.obj);
    }
}

assign(dest:Displayable, ref src:Displayable) {
    dest = copy(src);
}

display(x:Displayable) {
    x.display(x.obj^);
}

displayln(x) { display(x); println(); }

main() {
    var v = Vector[Displayable]();
    push(v, Displayable(1));
    push(v, Displayable(3.2));
    push(v, Displayable("Hi!"));
    for (i in range(3))
        push(v, Displayable(v));
    for (x in v)
        displayln(x);
}
